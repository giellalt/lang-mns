!! =================================== !
!! !!!The Mansi morphophonological/twolc rule file 
!! =================================== !

Alphabet
а б в г д е ё ж з и й к л м н о п р с т 
у ф х ц ч ш щ ъ ы ь э ю я ӈ 
А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т 
У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я Ӈ

а̄ е̄ ӣ о̄ ӯ ы̄ э̄ ю̄ я̄ ё̄ А̄ Е̄ Ӣ О̄ Ӯ Ы̄ ¯Э Ю̄ Я̄     !!= @CODE@ long vowels

ы2:ы ! floating vowel


!! Archiphones
%{АЯØ%}:0	!!= __@CODE@__ 
%{ЫИØ%}:0	!!= __@CODE@__ 
%{ЫØ%}:0	!!= __@CODE@__ +Loc
%{УЮØ%}:0	!!= __@CODE@__ 
%{ТØ%}:0	!!= __@CODE@__ 
%{ЭЕ%}:0	!!= __@CODE@__ PxSg3
%{ЭЕLong%}:0	!!= __@CODE@__ PxDu3


!! Trigger

%^PxDu2:0	!!= __@CODE@__ 
%^PxDu3:0	!!= __@CODE@__ 
%^PxPl2:0	!!= __@CODE@__ 

!! BOUNDARIES
 %> # 
;

Sets

 Vow = а е и о у ы э ю я
       А Е И О У Ы Э Ю Я
       а̄ е̄ ӣ о̄ ӯ ы̄ э̄ ю̄ я̄ ё̄
       А̄ Е̄ Ӣ О̄ Ӯ Ы̄ ¯Э Ю̄ Я̄ ;
       
 VowNoI = а е   о у э ю я
          А Е   О У Э Ю Я
          а̄ е̄ ӣ о̄ ӯ ы̄ э̄ ю̄ я̄ ё̄
          А̄ Е̄ Ӣ О̄ Ӯ Ы̄ ¯Э Ю̄ Я̄ ;


SVow = а е ё и о у ы э ю я ;

LVow = а̄ е̄ ӣ о̄ ӯ ы̄ э̄ ю̄ я̄ ё̄ ;

 CnsSoft = щ
           Щ  ;

 CnsNonAlv = б в г ж к м п ф х ц ч ш ӈ
             Б В Г Ж К М П Ф Х Ц Ч Ш Ӈ  ;

 CnsAlv = д з л н р с т
          Д З Л Н Р С Т ;

 Cns = CnsSoft CnsNonAlv CnsAlv ;


Definitions

! PostSyncStem  =  :Cns  %+:0 ;
PostSyncStem  =  :Cns   ;

 
Rules



"Remove fleeting vowel Ы at end of syncopating stem: when after morpheme boundary, and followed by a VOWEL"
ы2:0   <=>   _  PostSyncStem (ь:)  :Vow ;


!€grís%>s
!€grís00

!$grís%>s
!$grís0s

! "Palatalization of suffix vowels"
! Vx:Vy <=> ь: _ ;
! 	where Vx in ( ы )
! 	      Vy in ( и )
! 	      matched ;

! "Delete Palatalisation Mark"
! ь:0 <=> _ Vow: ;

!! !!VOWEL DELETION
"%{ЫИØ%}:0"
!! __@RULENAME@__
 %{ЫИØ%}:0 <=>  VowNoI:  %> _ ( %^PxDu2: | %^PxPl2: ) [ г | н | т ] ;
               [ и: | ы: ] %> _ ( %^PxDu2: | %^PxPl2: ) [ н | т ]  ;
!! JACK
!?  VowNoI:  %> _ ( %^PxDu2: | %^PxPl2: ) %> [ г | н | т ] ;
!€# рума%>%{ЫИØ%}н%>%{ЫØ%}т
!€0 рума00н00т

"%{ЫØ%}:0"
!! __@RULENAME@__
 %{ЫØ%}:0 <=> :Vow ((%>) :0+ ) (Cns (ь)) %> _ т  ;
!! JACK
!€# рума%>%{ЫИØ%}н%>%{ЫØ%}т
!€0 рума00н00т


"%{УЮØ%}:0"
!! __@RULENAME@__
 %{УЮØ%}:0 <=> Vow %> _ м ;
               VowNoI %> _ в ;

"%{АЯØ%}:0"
!! __@RULENAME@__
 %{АЯØ%}:0 <=> VowNoI %> _  ;

"ы:0"
!! __@RULENAME@__
 ы:0 <=> # Cns* Vow Cns _ Cns %> [ %{ЫИØ%}: | %{ЫØ%}: ] т  ;

"и:0"
!! __@RULENAME@__
 и:0 <=> # Cns* Vow Cns _ Cns %> [ %{ЫИØ%}: | %{ЫØ%}: ] т  ;

"ь:0"
!! __@RULENAME@__
 ь:0 <=> _ %> [ %{ЫИØ%}: | %{УЮØ%}: | %{АЯØ%}: | %{ТØ%}: [ %{ЭЕ%}: | %{ЭЕLong%}: ] ]  ;
!€# ха̄ль%>%{ТØ%}%{ЭЕ%}
!€0 ха̄л000е

"%{ТØ%}:0"
!! __@RULENAME@__
 %{ТØ%}:0 <=> Cns (ь: ) %> _ [ %{ЭЕ%}: | %{ЭЕLong%}: ]  ;
! # Cns* (Vow Cns) Vow Cns (ь: ) %> _ [ %{ЭЕ%}: | %{ЭЕLong%}: ]  ;
!€# ха̄ль%>%{ТØ%}%{ЭЕ%}
!€0 ха̄л000е

!! VOWEL FROM ARCHIVOWEL
"%{ЫИØ%}:ы"
!! __@RULENAME@__
 %{ЫИØ%}:ы <=> [ и | ы ] %> 0: _ г  ;
               Cns %> _  [ г | н | т ] ;
!(2007: 30) short elsewhere long??               Cns %> _ %^PxDu2: н  ;
!               Cns %> _ (%^PxDu2: ) [ г | н | т ] ;

"%{ЫИØ%}:и"
!! __@RULENAME@__
 %{ЫИØ%}:и <=> [ ь: | CnsSoft ] %> _ [ г | н | т ] ;
                [ и | ы ] %> 0: _ %^PxDu2: н  ;
"%{ЫØ%}:ы"
!! __@RULENAME@__
 %{ЫØ%}:ы <=> Cns (ы:0|и:0) [ CnsAlv | CnsNonAlv ] %> _ т  ;
!€# 

"%{ЭЕ%}:э"
!! __@RULENAME@__
 %{ЭЕ%}:э <=> [ CnsAlv | Vow ] %> %{ТØ%}: _ ;

"%{ЭЕ%}:е"
!! __@RULENAME@__
 %{ЭЕ%}:е <=> [ CnsSoft | CnsNonAlv | ь: ] %> %{ТØ%}: _  ;
!€# ха̄ль%>%{ТØ%%{ЭЕ%}
!€0 ха̄л000е

"%{УЮØ%}:у"
!! __@RULENAME@__
 %{УЮØ%}:у <=> Cns %> _ [ м | в ] ;

"%{УЮØ%}:ю"
!! __@RULENAME@__
 %{УЮØ%}:ю <=> ь: %> _ м ;
               [ и | ы | ь: ] %> _ в ;

"%{АЯØ%}:а"
!! __@RULENAME@__
 %{АЯØ%}:а <=> Cns %> _  ;

"%{АЯØ%}:я"
!! __@RULENAME@__
 %{АЯØ%}:я <=> [ и | ы | ь: ] %> _  ;



! !!VOWEL LENGTHENING
"%{ЫИØ%}:ы̄"
!! __@RULENAME@__
 %{ЫИØ%}:ы̄ <=>   [ CnsAlv | CnsNonAlv ]  %>  _ [ %^PxPl2: | %^PxDu2: ] ;
!(2007: 30 ) short  [ CnsAlv | CnsNonAlv ]  %>  _  %^PxPl2: ;


"%{ЫИØ%}:ӣ"
!! __@RULENAME@__
 %{ЫИØ%}:ӣ <=> [ ь: | CnsSoft  ] %> _ [ %^PxPl2: | %^PxDu2: ] ;
!          [ ь: | CnsSoft | CnsNonAlv ] %> _ [ %^PxDu2: ] ;


!"%{ЭЕ%}:э̄"
!!! __@RULENAME@__
! %{ЭЕ%}:э̄ <=> [ CnsAlv | Vow ] %> %{ТØ%}: _ %^PxDu3: ;

!"%{ЭЕ%}:е̄"
!!! __@RULENAME@__
! %{ЭЕ%}:е̄ <=>  [ CnsSoft | CnsNonAlv | ь:]  %> %{ТØ%}: _ %^PxDu3: ;
!!€# ха̄ль%>%{ТØ%}%{ЭЕ%}%^PxDu3н
!!€0 ха̄л000е̄0н

"%{ЭЕLong%}:э̄"
!! __@RULENAME@__
 %{ЭЕLong%}:э̄ <=> [ CnsAlv | Vow ] %> %{ТØ%}: _ ;
!€# аки%>%{ТØ%}%{ЭЕLong%}н
!€0 аки0тэ̄н

"%{ЭЕLong%}:е̄"
!! __@RULENAME@__
 %{ЭЕLong%}:е̄ <=> [ CnsSoft | CnsNonAlv | ь: ] %> %{ТØ%}: _ ;
!€# ха̄ль%>%{ТØ%}%{ЭLongЕ%н
!€0 ха̄л000е̄н

"а:а̄"
!! __@RULENAME@__
 а:а̄ <=> _ %> %{ЫИØ%}: %^PxPl2: ;
!! JACK
!€# рума%>%{ЫИØ%}%^PxPl2н
!€0 рума̄000н

"и:ӣ"
!! __@RULENAME@__
 и:ӣ <=> _ %> %{ЫИØ%}: %^PxDu2: ;

"ы:ы̄"
!! __@RULENAME@__
 ы:ы̄ <=> _ %> %{ЫИØ%}: %^PxDu2: ;


!! !!CONSONANTS
"%{ТØ%}:т"
!! __@RULENAME@__
 %{ТØ%}:т <=> Vow %> _ [ %{ЭЕ%}: | %{ЭЕLong%}: ]  ;


!! !!SANDHI

"0:й"
!! __@RULENAME@__
 0:й <=> [ и | ы ] %> _  %{ЫИØ%}: [ г | %^PxDu2: н ] ;


!
"Required %^PxDu2 loss"
!! __@RULENAME__
 %^PxDu2:0 => _ ;

"Required %^PxDu3 loss"
!! __@RULENAME__
 %^PxDu3:0 => _ ;

"Required %^PxPl2 loss"
!! __@RULENAME__
 %^PxPl2:0 => _ ;

