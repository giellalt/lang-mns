!! =================================== !
!! The Mansi morphophonological/twolc rules file !
!! =================================== !

Alphabet
а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я ӈ
А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я

ы2:ы ! floating vowel

а̄
е̄
ӣ
о̄
ӯ
ы̄
э̄
ю̄
я̄
А̄
Е̄
Ӣ
О̄
Ӯ
Ы̄
Э̄
Ю̄
Я̄

!! Archiphones
%{АЯØ%}:0	!!= __@CODE@__ 
%{ЫИØ%}:0	!!= __@CODE@__ 
%{ЫØ%}:0	!!= __@CODE@__ +Loc
%{УЮØ%}:0	!!= __@CODE@__ 
%{ТØ%}:0	!!= __@CODE@__ 
%{ЭЕ%}:0	!!= __@CODE@__ PxSg3


!! Trigger

%^PxDu2:0	!!= __@CODE@__ 
%^PxDu3:0	!!= __@CODE@__ 
%^PxPl2:0	!!= __@CODE@__ 

!! BOUNDARIES
 %> # 
;

Sets

 Vow = а е и о у ы э ю я
       А Е И О У Ы Э Ю Я
       а̄ е̄ ӣ о̄ ӯ ы̄ э̄ ю̄ я̄
       А̄ Е̄ Ӣ О̄ Ӯ Ы̄ Э̄ Ю̄ Я̄ ;
       
 VowNoI = а е о у э ю я
           А Е О У Э Ю Я
а̄ е̄ о̄ ӯ  э̄ ю̄ я̄
А̄ Е̄ О̄ Ӯ Э̄ Ю̄ Я̄
;

SVow = а е ё и о у ы э ю я ;

LVow = ā ē ё̄ ӣ ō ӯ ы̄ э̄ ю̄ я̄ ;

 Cns = б в г д ж з к л м н п р с т ф х ц ч ш щ ӈ
       Б В Г Д Ж З К Л М Н П Р С Т Ф Х Ц Ч Ш Щ Ӈ  ;


Definitions

! PostSyncStem  =  :Cns  %+:0 ;
PostSyncStem  =  :Cns   ;

 
Rules



"Remove fleeting vowel Ы at end of syncopating stem: when after morpheme boundary, and followed by a VOWEL"
ы2:0   <=>   _  PostSyncStem (ь:)  :Vow ;


!€grís%>s
!€grís00

!$grís%>s
!$grís0s

!! !!VOWEL DELETION
"%{ЫИØ%}:0"
!! __@RULENAME@__
 %{ЫИØ%}:0 <=>  VowNoI:  %> _ ( %^PxDu2: | %^PxPl2: ) [ г | н | т ] ;
               [ и: | ы: ] %> _ ( %^PxDu2: | %^PxPl2: ) [ н | т ]  ;
!! JACK
!?  VowNoI:  %> _ ( %^PxDu2: | %^PxPl2: ) %> [ г | н | т ] ;
!€# рума%>%{ЫИØ%}н%>%{ЫØ%}т
!€0 рума00н00т

"%{ЫØ%}:0"
!! __@RULENAME@__
 %{ЫØ%}:0 <=> :Vow ((%>) :0+ ) (Cns (ь)) %> _ т  ;
!! JACK
!€# рума%>%{ЫИØ%}н%>%{ЫØ%}т
!€0 рума00н00т


"%{УЮØ%}:0"
!! __@RULENAME@__
 %{УЮØ%}:0 <=> Vow %> _ м ;
               VowNoI %> _ в ;

"%{АЯØ%}:0"
!! __@RULENAME@__
 %{АЯØ%}:0 <=> VowNoI %> _ н ы л ;

"ы:0"
!! __@RULENAME@__
 ы:0 <=> # Cns* Vow Cns _ Cns %> [ %{ЫИØ%}: | %{ЫØ%}: ] т  ;

"и:0"
!! __@RULENAME@__
 и:0 <=> # Cns* Vow Cns _ Cns %> [ %{ЫИØ%}: | %{ЫØ%}: ] т  ;

"ь:0"
!! __@RULENAME@__
 ь:0 <=> _ %> [ %{ЫИØ%}: | %{УЮØ%}: | %{АЯØ%}: | %{ТØ%}: %{ЭЕ%}: ]  ;
!€# ха̄ль%>%{ТØ%}%{ЭЕ%}
!€0 ха̄л000е

"%{ТØ%}:0"
!! __@RULENAME@__
 %{ТØ%}:0 <=> # Cns* (Vow Cns) Vow Cns (ь: ) %> _ %{ЭЕ%}:  ;
!€# ха̄ль%>%{ТØ%}%{ЭЕ%}
!€0 ха̄л000е

!! VOWEL FROM ARCHIVOWEL
"%{ЫИØ%}:ы"
!! __@RULENAME@__
 %{ЫИØ%}:ы <=> [ и | ы ] %> 0: _ г  ;
               Cns %> _ (%^PxDu2: ) [ г | н | т ] ;

"%{ЫИØ%}:и"
!! __@RULENAME@__
 %{ЫИØ%}:и <=> ь: %> _ [ г | н | т ] ;

"%{ЫØ%}:ы"
!! __@RULENAME@__
 %{ЫØ%}:ы <=> Cns (ы:0|и:0) Cns %> _ т  ;
!%# 

"%{ЭЕ%}:э"
!! __@RULENAME@__
 %{ЭЕ%}:э <=> [ Cns | Vow ] %> %{ТØ%}: _ [ %> | # ] ;

"%{ЭЕ%}:е"
!! __@RULENAME@__
 %{ЭЕ%}:е <=> ь: %> %{ТØ%}: _ [ %> | # ] ;
!€# ха̄ль%>%{ТØ%}%{ЭЕ%}
!€0 ха̄л000е

"%{УЮØ%}:у"
!! __@RULENAME@__
 %{УЮØ%}:у <=> Cns %> _ [ м | в ] ;

"%{УЮØ%}:ю"
!! __@RULENAME@__
 %{УЮØ%}:ю <=> ь: %> _ м ;
               [ и | ы | ь: ] %> _ в ;

"%{АЯØ%}:а"
!! __@RULENAME@__
 %{АЯØ%}:а <=> Cns %> _ н ы л ;

"%{АЯØ%}:я"
!! __@RULENAME@__
 %{АЯØ%}:я <=> [ и | ы | ь: ] %> _ н ы л ;



! !!VOWEL LENGTHENING
"%{ЫИØ%}:ы̄"
!! __@RULENAME@__
 %{ЫИØ%}:ы̄ <=>  Cns  %>  _ %^PxPl2: ;

"%{ЫИØ%}:ӣ"
!! __@RULENAME@__
 %{ЫИØ%}:ӣ <=> [ ь: ] %> _ [ %^PxPl2: ] ;
          [ ь: ] %> _ [ %^PxDu2: ] ;
!          [ ь: |  ӈ ] %> _ [ %^PxDu2: ] ;

"%{ЭЕ%}:э̄"
!! __@RULENAME@__
 %{ЭЕ%}:э̄ <=> [ Cns | Vow ] %> %{ТØ%}: _ %^PxDu3: ;

"%{ЭЕ%}:е̄"
!! __@RULENAME@__
 %{ЭЕ%}:е̄ <=> ь: %> %{ТØ%}: _ %^PxDu3: ;
!€# ха̄ль%>%{ТØ%}%{ЭЕ%}%^PxDu2н
!€0 ха̄л000е̄0н

"а:а̄"
!! __@RULENAME@__
 а:а̄ <=> _ %> %{ЫИØ%}: %^PxPl2: ;
!! JACK
!€# рума%>%{ЫИØ%}%^PxPl2н
!€0 рума̄000н

"и:ӣ"
!! __@RULENAME@__
 и:ӣ <=> _ %> %{ЫИØ%}: %^PxDu2: ;

"ы:ы̄"
!! __@RULENAME@__
 ы:ы̄ <=> _ %> %{ЫИØ%}: %^PxDu2: ;


!! !!CONSONANTS
"%{ТØ%}:т"
!! __@RULENAME@__
 %{ТØ%}:т <=> Vow %> _ %{ЭЕ%}:  ;


!! !!SANDHI

"0:й"
!! __@RULENAME@__
 0:й <=> [ и | ы ] %> _  %{ЫИØ%}: г ;


!
"Required %^PxDu2 loss"
!! __@RULENAME__
 %^PxDu2:0 => _ ;

"Required %^PxDu3 loss"
!! __@RULENAME__
 %^PxDu3:0 => _ ;

"Required %^PxPl2 loss"
!! __@RULENAME__
 %^PxPl2:0 => _ ;

