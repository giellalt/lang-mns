! =============================================== !
!! # The Mansi morphophonological/twolc rule file
! =============================================== !

!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-mns/blob/main/src/fst/phonology.twolc) 

!! Скрибник Е.К. & К.В. Афанасьева 2007:
!! Практически курс манийского языка, Часть 1. Ханты-Мансийск. Полиграфист.


!! ## Alphabet, sets and definitions
!! ### Alphabet
Alphabet
!! Basic letters:
а б в г д е ё ж з и й к л м н о п р с т  !!≈ * **@CODE@** 
у ф х ц ч ш щ ъ ы ь э ю я ӈ			     !!≈ * **@CODE@** 
А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т  !!≈ * **@CODE@** 
У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я Ӈ			     !!≈ * **@CODE@** 

!! Letters with diacritical marks:
!! Precomposed letters are used when available, otherwise non-composed ones.

ӣ ӯ !!≈ * **@CODE@** precomposed long vowels
Ӣ Ӯ !!≈ * **@CODE@** precomposed long vowels
а̄ е̄ о̄ ы̄ э̄ ю̄ я̄ ё̄ А̄ Е̄ О̄ Ы̄ Э̄ Ю̄ Я̄     !!≈ * **@CODE@** non-precomposed long vowels


!! #### Archiphones

%{аяØ%}:0	!!≈ * **@CODE@** PxPl3 %{аяØ%}ныл
%{ыиØ%}:0	!!≈ * **@CODE@** Loc and Ins and ...
%{уюØ%}:0	!!≈ * **@CODE@** suffix onset vowel
%{ЫИ%}:и	!!≈ * **@CODE@**
%{ЫИ%}:ы	!!≈ * **@CODE@**
%{ЭЕLong%}:е̄	!!≈ * **@CODE@** +V+Ind+Prs+ScSg1
%{ЭЕLong%}:э̄	!!≈ * **@CODE@** +V+Ind+Prs+ScSg1
%{ЭЕLong%}:е	!!≈ * **@CODE@** +V+Ind+Prs+ScSg1 VU
%{ЭЕLong%}:э	!!≈ * **@CODE@** +V+Ind+Prs+ScSg1 VU
%{ЭЕ%}:е	!!≈ * **@CODE@** PxSg3
%{ЭЕ%}:э	!!≈ * **@CODE@** PxSg3
%{УЮØ%}:0	!!≈ * **@CODE@**
%{йØ%}:0	!!≈ * **@CODE@** ыг
%{Øы%}:0	!!≈ * **@CODE@** Loc
%{ыØ%}:ы	!!≈ * **@CODE@** specific floating vowel 
%{иØ%}:и	!!≈ * **@CODE@** specific floating vowel 
%{уØ%}:у	!!≈ * **@CODE@** specific floating vowel +N+Sg+PxDu1+Nom %{уØ%} м е̄ н
%{тØ%}:0	!!≈ * **@CODE@** PxSg3, Ins



!! #### Triggers
%{VO%}:0	!!≈ __@CODE@__ Stem ending in vowel other than и ы
%{VI%}:0	!!≈ __@CODE@__ Stem ending in vowel и or ы
%{SYNCH%}:0	!!≈ __@CODE@__ Stem with syncope with и, ы, у hard
%{SYNCS%}:0	!!≈ __@CODE@__ Stem with syncope with и, ы, у soft
%{NOSYNCH%}:0	!!≈ __@CODE@__ Stem without syncope with и, ы, у hard
%{NOSYNCS%}:0	!!≈ __@CODE@__ Stem without syncope with и, ы, у soft
%{VCH%}:0	!!≈ __@CODE@__ Stem ending in single hard consonant
%{VCCH%}:0	!!≈ __@CODE@__ Stem ending in hard consonant cluster
%{VCS%}:0	!!≈ __@CODE@__ Stem ending in single soft consonant
%{VCCS%}:0	!!≈ __@CODE@__ Stem ending in soft consonant cluster

%{VA%}:0	!!≈ __@CODE@__ -аӈкве verb
%{VU%}:0	!!≈ __@CODE@__ -уӈкве verb

%^PxDu2:0	!!≈ * **@CODE@**
%^PxDu3:0	!!≈ * **@CODE@**
%^PxPl2:0	!!≈ * **@CODE@**
%^RmVow:0	!!≈ * **@CODE@** Word-final vowel removal
%^Syncope:0	!!≈ * **@CODE@** This triggers syncope with no reason to place special letters in the stem.

!! #### Literal quotes and angles
!! These must be escaped (cf morpheme boundaries below):

 »7     !! * »7
 «7     !! * «7
 %[%>%] !! * >
 %[%<%] !! * <

!! ### Boundaries

 %> # !!≈  **@CODE@** for suffix and word end

!! #### Morpheme boundaries:
 «  !!≈ * **@CODE@** = Derivational prefix
 »  !!≈ * **@CODE@** = Derivational suffix
 %< !!≈ * **@CODE@** = Inflectional prefix
 %> !!≈ * **@CODE@** = Inflectional suffix
 #  !!≈ * **@CODE@** = Word boundary for both lexicalised and dynamic compounds
 %^ !!≈ * **@CODE@** = (exceptional) soft hyphenation point
 %- !!≈ * **@CODE@** = Hyphen for question particle
 ð  !!≈ * **@CODE@** = Compounding rules

!! End of alphabet definitions
 ;

!! ### Sets
Sets

 Vow = а е и о у ы э ю я			   !!≈ * **@CODE@**
       А Е И О У Ы Э Ю Я			   !!≈  **@CODE@**
       а̄ е̄ ӣ о̄ ӯ ы̄ э̄ ю̄ я̄ ё̄	   !!≈  **@CODE@**
       А̄ Е̄ Ӣ О̄ Ӯ Ы̄ Э̄ Ю̄ Я̄ Ё̄ ;  !!≈  **@CODE@**
 VowNoI = а е   о у э ю я				   !!≈ * **@CODE@**
          А Е   О У Э Ю Я				   !!≈  **@CODE@**
          а̄ е̄ ӣ о̄ ӯ ы̄ э̄ ю̄ я̄ ё̄	   !!≈  **@CODE@**
          А̄ Е̄ Ӣ О̄ Ӯ Ы̄ Э̄ Ю̄ Я̄ Ё̄ ;   !!≈  **@CODE@**
  PalVow = е ё и ю я е̄ ӣ ю̄ я̄ ё̄ ;  !!≈ * **@CODE@**
SVow = а е ё и о у ы э ю я ; !!≈ * **@CODE@**
LVow = а̄ е̄ ё̄ ӣ о̄ ӯ ы̄ э̄ ю̄ я̄ ; !!≈ * **@CODE@**
 CnsSoft = щ Щ  ;  !!≈ * **@CODE@**
 CnsNonAlv = б в г ж к м п ф х ц ч ш ӈ   !!≈ * **@CODE@**
             Б В Г Ж К М П Ф Х Ц Ч Ш Ӈ  ; !!≈  **@CODE@**
 CnsAlv = д з л н с т !!≈ * **@CODE@**
          Д З Л Н С Т ; !!≈  **@CODE@**
 Cns = CnsSoft CnsNonAlv CnsAlv  р Р й Й;  !!≈ * **@CODE@**


!! ### Definitions
Definitions

Pal = [%{VCCS%}:|%{VCS%}:|%{SYNCS%}:|%{NOSYNCS%}:] ;


! PostSyncStem  =  :Cns  %+:0 ;
PostSyncStem  =  :Cns   ;  !!≈ * **@CODE@** 


!! ## Rules
Rules

"Syncope for ы"
!! **RULE: @RULENAME@**  
 ы:0 <=> Cns _ (Cns) [Cns - й] [%{SYNCH%}:|%{SYNCS%}:] %> [ы|а|%{аяØ%}:|%{уюØ%}:|%{ЭЕ%}:] ;
  %{VA%}: %> %{аяØ%}: %> _ ;
  [%{VO%}:| %{VI%}:]  %> _ [т] ; !Plural
!!€ ловиньт{VA}>{аяØ}>ыс
!!€ ловиньт0>а>0с
!! павыл+N+Pl+Nom:
!!€ павыл{SYNCH}>ыт
!!€ пав0л0>ыт
!!
!!€ хӯрыг{SYNCH}>ыт
!! кол+N+Pl+Nom: **house/koti**
!!€ кол{VCH}>ыт
!!€ кол0>ыт
!! кол+N+Sg+Loc: **house/koti**
!!€ кол{VCH}>{ыиØ}т
!!€ кол0>0т
!! класс+N+Sg+Loc:
!!€ класс{VCCH}>{ыиØ}т
!!€ класс0>ыт
!! класс+N+Pl+Nom:
!!€ класс{VCCH}>ыт
!!€ класс0>ыт

"Syncope for и"
!! **RULE: @RULENAME@**  
 и:0 <=> Cns _ (Cns) [Cns - й] [%{SYNCH%}:|%{SYNCS%}:] %> [%{ыиØ%}:|а|%{аяØ%}:|%{уюØ%}:|%{ЭЕ%}:] ;

"Syncope for у"
!! **RULE: @RULENAME@**  
 у:0 <=> Cns _ (Cns) [Cns - й] [%{SYNCH%}:|%{SYNCS%}:] %> [%{ыиØ%}:|а|%{аяØ%}:|%{уюØ%}:|%{ЭЕ%}:] ;


!! ла̄тыӈ+N+Sg+PxPl1+Nom:  **language/kieli**
!!€ ла̄тыӈ{SYNCH}>{уюØ}в
!!€ ла̄т0ӈ0>ув
!! яныг+N+Sg+Loc:
!!€ яныг{SYNCH}>{ыиØ}т
!!€ ян0г0>ыт


!! #### Tests:
 
! !€ апа>{йØ}ыт
! !€ апа>00т
! ! аки+N+Du+Nom:  __paternal uncle younger than father__
! !€ аки>{йØ}ыг
! !€ аки>0иг
!!€ ха̄п>{йØ}ыт
!!€ ха̄п>0ыт
!!€ апа>ныл
!!€ апа>ныл
!!€ паль{VCS}>ныл
!!€ паль0>ныл
! !€ ха̄п>ыныл
! !€ ха̄п>0ныл  

!! #### Tests:

!!€ паль{VCS}>{йØ}{ыиØ}т
!!€ пал00>0ит

!! #### Tests:

!!€ а̄ны{VI}>{йØ}{ыиØ}г
!!€ а̄ны0>0иг
!! аки+N+Sg+Tra: __as an uncle__
!!€ аки{VI}>{йØ}{ыиØ}г
!!€ аки0>0иг


!%{ыиØ%}:ы this is for Loc
"Vow ы in loc appears"
%{ыиØ%}:ы <=>
     [%{VCCH%}:] %> _ [т|н|л|я] ;
!! павыл+N+Sg+Loc:
!!€ павыл{SYNCH}>{ыиØ}т
!!€ павыл0>0т
!!
!! я̄ӈк+N+Sg+Loc: **ice/jää**
!!€ я̄ӈк{VCCH}>{ыиØ}т
!!€ я̄ӈк0>ыт

"Vow и in loc appears"
%{ыиØ%}:и <=>
     Pal %> _ [т|н|л|я] ;
     
!!ансамбль+N+Sg+Loc: **band/yhtye**
!!€ ансамбль{VCCS}>{ыиØ}т
!!€ ансамбл00>ит

"Vow и in plural ы to и"
ы:и <=> [%{VCCS%}:|%{VCS%}:|%{SYNCS%}:] %> _ [т] ;
!!ансамбль+N+Pl+Nom: **band/yhtye**
!!€ ансамбль{VCCS}>ыт
!!€ ансамбл00>ит

!! #### Tests:
!ве̄рмуӈкве+Ind+Prs+OcPl3+ScSg3:
!!€ ве̄рмы%>%{ЫИ%}янэ
!!€ ве̄рм0%>иянэ

! +V+Ind+Prs+ScSg1:
! € >%{ЭЕLong%}гум

!! #### Tests:

!!€ вармаль{VCS}>{ыиØ}т
!!€ вармал00>ит
!!$ та̄л{VCS}>{ыиØ}г
!!$ та̄л0>иг


!! #### Tests:

!!€ са̄лы{VI}>{аяØ}гын
!!€ са̄лы0>ягын

!! #### Tests:

!!€ са̄лы{VI}>{уюØ}м
!!€ са̄лы0>0м
!!€ аки{VI}>{уюØ}м
!!€ аки0>0м



"Soft sign deletion for и suffixes"
!! **RULE: @RULENAME@**  2023.02
ь:0 <=>
    _ [%{VCCS%}:|%{VCS%}:|%{SYNCS%}:] %>  %{ыиØ%}: [т|н|л]; !! ансамбль+N+Sg+Loc: **band/yhtye**
    _ [%{VCCS%}:|%{VCS%}:|%{SYNCS%}:] %>  ы: [т]; !! ансамбль+N+Pl+Nom: **band/yhtye**
    _ [%{VCCS%}:|%{VCS%}:|%{SYNCS%}:] %> (%{йØ%}:0)  %{ыиØ%}: [г]; !! ансамбль+N+Sg+Loc: **band/yhtye**
!!€ ансамбль{VCCS}>{ыиØ}т
!!€ ансамбл00>ит
    _ [%{VU%}:|%{VA%}:] %> [%{уюØ%}:| %{аяØ%}: ]; !! XX+V+Inf: ****

!Cns _ (ы:0) %> (%{йØ%}:0) [:PalVow| %{тØ%}: [ %{ЭЕ%}: | %{ЭЕLong%}: ]] ;
!above

!! #### Tests:

!! ха̄ль+N+Sg+Nom+PxSg3
!!€ ха̄ль{VCS}>{тØ}{ЭЕ}
!!€ ха̄л00>0е

!!€ паль{VCS}>{йØ}{ыиØ}г
!!€ пал00>0иг
! In light of the yaml file saali, this seems wrong, we want са̄лыиг, нот са̄лыйыг
!"%{йØ%}:й instertion for vowel initial suffixes on ы final stems"
!%{йØ%}:й <=> [ы|и] %> _ ы г ;
!€ са̄лы{VI}>0{ыиØ}г
!€ са̄лы0>йыг

!!TESTS
!echo 'л а̄ т {ыØ}:0 ӈ > {уюØ}:у в' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'х а̄ л ь:0 > {тØ}:0 {ЭЕ}:е' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'я н ы > {тØ}:т ы л' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'о к р у г > {уюØ}:у в' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'ла̄тыӈ+N+Sg+Nom' | hfst-lookup src/morphology/lexicon.hfst

!! павыл+N+Pl+Nom:
!!€ павыл{SYNCH}>ыт
!!€ пав0л0>ыт



"%{аяØ%}:0"
!! __@RULENAME@__ deletes а/я after VO vowel stems
%{аяØ%}:0 <=> %{VO%}:  %> _  ;
!VowNoI  %> _  ;


"%{аяØ%}:а"
!! __@RULENAME@__
 %{аяØ%}:а <=> [%{SYNCH%}: | %{NOSYNCH%}: | %{VCH%}: | щ %{VCS%}:| %{VCCH%}: | [CnsSoft | CnsAlv | CnsNonAlv| р] [%{VA%}:|%{VU%}:]]  %> _  ;
! Cns %> _  ;

"%{аяØ%}:я"
!! __@RULENAME@__
  %{аяØ%}:я <=> [%{VI%}: | Pal | [ь: | й:] [%{VA%}:|%{VU%}:]]  %> _  ;
  ! [ и | ы | ь: ] %> _  ;


"%{уюØ%}:0"
!! __@RULENAME@__ deletes а/я after VO vowel stems
%{уюØ%}:0 <=> %{VO%}:  %> _  ;
!VowNoI  %> _  ;


"%{уюØ%}:у"
!! __@RULENAME@__
 %{уюØ%}:у <=> [%{SYNCH%}: | %{NOSYNCH%}: | %{VCH%}: | %{VCCH%}: | [CnsSoft | CnsAlv| CnsNonAlv | р] %{VU%}: ]  %> _  ;
! Cns %> _  ;

"%{уюØ%}:ю"
!! __@RULENAME@__
  %{уюØ%}:ю <=> [%{VI%}: | Pal | [й:| ь: ] %{VU%}:]  %> _  ;
  ! [ и | ы | ь: ] %> _  ;
!! коюӈкве+V+Inf: **track/jäljittää, vuottaa**
!!€ кой{VU}>{уюØ}ӈкве
!!€ ко00>юӈкве

"%{ЭЕLong%}:э̄"
!! __@RULENAME@__
 %{ЭЕLong%}:э̄ <=> CnsAlv %{VA%}: %> (%{тØ%}:) _ ;
!! аки
!!€ аки{VI}>%{тØ%}%{ЭЕLong%}н
!!€ аки0>тэ̄н

 "%{ЭЕLong%}:е̄"
 !! __@RULENAME@__
  %{ЭЕLong%}:е̄ <=> [ CnsSoft | CnsNonAlv | ь: | й: ] %{VA%}: %> _ ;
 !!€ ха̄ль%>%{тØ%}%{ЭLongЕ%н
 !!€ ха̄л0%>0е̄н

"%{ЭЕLong%}:э"
!! __@RULENAME@__
 %{ЭЕLong%}:э <=> CnsAlv %{VU%}: %> _ ;
 !!€ тот{VU}>{ЭЕLong}в
 !!€ тот0>эв

 "%{ЭЕLong%}:е"
 !! __@RULENAME@__
  %{ЭЕLong%}:е <=> [ CnsSoft | CnsNonAlv | ь: | й: ] %{VU%}: %> _ ;
 !!€ ха̄ль%>%{тØ%}%{ЭLongЕ%н
 !!€ ха̄л0%>0е̄н

"{ЭЕ}:э"
!! __@RULENAME@__
%{ЭЕ%}:э <=> [ CnsAlv - р | Vow ] [%{VU%}:|%{VA%}:] %> _ ;
             Vow %{VO%}: %> %{тØ%}: _ ;
"{ЭЕ}:е"
!! __@RULENAME@__
 %{ЭЕ%}:е <=> [ CnsSoft | р | CnsNonAlv | ь: | й:] [%{VU%}:|%{VA%}:] %>  _  ;
!!€ ха̄ль%>%{тØ%}%{ЭЕ%}
!!€ ха̄л0%>0е
!!€ потыр%^Syncope%>%{тØ%}%{ЭЕ%}
!!€ пот0р0%>0е

"%{ЫИ%}:ы"
!! __@RULENAME@__
%{ЫИ%}:ы <=> [ CnsAlv - р | Vow ] [%{VU%}:|%{VA%}:] %> _ ;
!! тотуӈкве+V+Ind+Prs+ScDu1+OcDu3:
!!€ тот{VU}>{ЫИ}ягмен
!!€ тот0>ыягмен
!! tотуӈкве+V+Ind+Prs+ScPl1+OcSg3:
!!€ тот{VU}>{ЫИ}лӯв
!!€ тот0>ылӯв
!lang-mns jackrueter$ echo 'т о т {VU}:0 > {ЫИ}:ы л ӯ в'| hfst-pair-test src/fst/phonology.compose.hfst

"%{ЫИ%}:и"
!! __@RULENAME@__
 %{ЫИ%}:и <=> [ CnsSoft | р | CnsNonAlv | ь: | й: ] [%{VU%}:|%{VA%}:] %>  _  ;

!! ### CONSONANTS
"й:0"
!! __@RULENAME@__
 й:0 <=> _ [%{VCS%}:| %{VU%}: | %{VA%}: ] %> [%{ЫИ%}:|%{ыиØ%}:|%{ЭЕ%}:|%{ЭЕLong%}:|%{уюØ%}:|%{аяØ%}:] ; 
!! коюӈкве+V+Inf: ****
!!€ кой{VU}>{уюØ}ӈкве
!!€ ко00>юӈкве

! "%{тØ%}:0"
! ! __@RULENAME@__
!  %{тØ%}:0 <=> Cns (ь: ) (%^Syncope:) %> _ [ %{ЭЕ%}: | %{ЭЕLong%}: | ы л ]  ;

! # Cns* (Vow Cns) Vow Cns (ь: ) %> _ [ %{ЭЕ%}: | %{ЭЕLong%}: ]  ;
!! ха̄ль+N+Sg+PxSg3+Nom
!!€ ха̄ль%>%{тØ%}%{ЭЕ%}
!!€ ха̄л0%>0е
!! кол+N+Sg+Ins:
!!€ кол%>%{тØ%}ыл
!!€ кол%>0ыл
!! яныт+N+Sg+Ins:
!!€ яныт>{тØ}ыл
!!€ яныт>0ыл
!! потыр+N+Sg+PxSg3+Nom
!!€ потыр>{тØ}{ЭЕ}
!!€ пот0р>0е
! VowNoI %>  _ ы: л ;
!! э̄ква+N+Sg+Ins
!!€ э̄ква>{тØ}ыл
!!€ э̄ква>00л


"%{тØ%}:т"
! __@RULENAME@__
%{тØ%}:т <=> Vow %{VO%}: %> _ [ %{ЭЕ%}: | %{ЭЕLong%}: ]  ;
             ы %{VI%}: %> _ %{ыиØ%}: л ;


!€ a
!€ b

!TOS !!€ потыр%^Syncope%>%{УЮØ%}м
!TOS !!€ пот0р0%>ум
!TOS !!€ потыр%^Syncope%>%{тØ%}%{ЭЕ%}
!TOS !!€ пот0р0%>0е
!TOS !!€ потыр%^Syncope%>%{ЫИØ%}т
!TOS !!€ пот0р0%>ыт
!TOS !!€ потыр%^Syncope%>0ме̄н
!TOS !!€ пот0р0%>уме̄н

!TOS !!€ э̄ква%>%{тØ%}ыл
!TOS !!€ э̄ква%>00л


!TOS !!€ па̄рт%>%{ЫИØ%}г%>%{ыØ%}н
!TOS !!€ па̄рт%>ыг%>0н
!TOS !!€ ха̄п%>%{ыØ%}н
!TOS !!€ ха̄п%>0н

!TOS !!€ па̄рт%>%{ыØ%}н
!TOS !!€ па̄рт%>ын
!TOS !!€ потыр%^Syncope%>%{ыØ%}н
!TOS !!€ пот0р0%>ын

!TOS !!€ рума%>%{ЫИØ%}%^PxPl2н
!TOS !!€ рума̄%>00н

!TOS !!€ потыр%>%{уØ%}ме̄н
!TOS !!€ пот0р%>уме̄н

