! =============================================== !
!! # The Mansi morphophonological/twolc rule file
! =============================================== !

!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-mns/blob/main/src/fst/phonology.twolc) 

!! Скрибник Е.К. & К.В. Афанасьева 2007:
!! Практически курс манийского языка, Часть 1. Ханты-Мансийск. Полиграфист.


!! ## Alphabet, sets and definitions
!! ### Alphabet
Alphabet
!! Basic letters:
а б в г д е ё ж з и й к л м н о п р с т  !!≈ * **@CODE@** 
у ф х ц ч ш щ ъ ы ь э ю я ӈ			     !!≈ * **@CODE@** 
А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т  !!≈ * **@CODE@** 
У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я Ӈ			     !!≈ * **@CODE@** 

!! Letters with diacritical marks:
!! Precomposed letters are used when available, otherwise non-composed ones.

ӣ ӯ !!≈ * **@CODE@** precomposed long vowels
Ӣ Ӯ !!≈ * **@CODE@** precomposed long vowels
а̄ е̄ о̄ ы̄ э̄ ю̄ я̄ ё̄ А̄ Е̄ О̄ Ы̄ Э̄ Ю̄ Я̄     !!≈ * **@CODE@** non-precomposed long vowels

ҥ Ҥ

!! #### Archiphones

%{аяØ%}:0	!!≈ * **@CODE@** PxPl3 %{аяØ%}ныл
%{ыиØ%}:0	!!≈ * **@CODE@** Loc and Ins and ...
%{уюØ%}:0	!!≈ * **@CODE@** suffix onset vowel
%{ЫИ%}:и	!!≈ * **@CODE@**
%{ЫИ%}:ы	!!≈ * **@CODE@**
%{аяLong%}:а̄	!!≈ * **@CODE@** ScPl3+OcSg3 %{аяLong%}ныл
%{ЭЕLong%}:е̄	!!≈ * **@CODE@** +V+Ind+Prs+ScSg1
%{ЭЕLong%}:э̄	!!≈ * **@CODE@** +V+Ind+Prs+ScSg1
!%{ЭЕLong%}:е	!!≈ * **@CODE@** +V+Ind+Prs+ScSg1 VU
!%{ЭЕLong%}:э	!!≈ * **@CODE@** +V+Ind+Prs+ScSg1 VU
%{ЭЕ%}:е	!!≈ * **@CODE@** PxSg3
%{ЭЕ%}:э	!!≈ * **@CODE@** PxSg3
%{УЮØ%}:0	!!≈ * **@CODE@**
%{йØ%}:0	!!≈ * **@CODE@** ыг
%{Øы%}:0	!!≈ * **@CODE@** Loc
%{ыØ%}:ы	!!≈ * **@CODE@** specific floating vowel 
%{иØ%}:и	!!≈ * **@CODE@** specific floating vowel 
%{уØ%}:у	!!≈ * **@CODE@** specific floating vowel +N+Sg+PxDu1+Nom %{уØ%} м е̄ н
%{тØ%}:0	!!≈ * **@CODE@** PxSg3, Ins



!! #### Triggers
%{VO%}:0	!!≈ __@CODE@__ Stem ending in vowel other than и ы
%{VI%}:0	!!≈ __@CODE@__ Stem ending in vowel и or ы
%{SYNCH%}:0	!!≈ __@CODE@__ Stem with syncope with и, ы, у hard
%{SYNCS%}:0	!!≈ __@CODE@__ Stem with syncope with и, ы, у soft
%{NOSYNCH%}:0	!!≈ __@CODE@__ Stem without syncope with и, ы, у hard
%{NOSYNCS%}:0	!!≈ __@CODE@__ Stem without syncope with и, ы, у soft
%{VCH%}:0	!!≈ __@CODE@__ Stem ending in single hard consonant
%{VCCH%}:0	!!≈ __@CODE@__ Stem ending in hard consonant cluster
%{VCS%}:0	!!≈ __@CODE@__ Stem ending in single soft consonant
%{VCCS%}:0	!!≈ __@CODE@__ Stem ending in soft consonant cluster

%{VA%}:0	!!≈ __@CODE@__ -аӈкве verb
%{VU%}:0	!!≈ __@CODE@__ -уӈкве verb

%^PxDu2:0	!!≈ * **@CODE@**
%^PxDu3:0	!!≈ * **@CODE@**
%^PxPl2:0	!!≈ * **@CODE@**
%^RmVow:0	!!≈ * **@CODE@** Word-final vowel removal
%^Syncope:0	!!≈ * **@CODE@** This triggers syncope with no reason to place special letters in the stem.

!! #### Literal quotes and angles
!! These must be escaped (cf morpheme boundaries below):

 »7     !! * »7
 «7     !! * «7
 %[%>%] !! * >
 %[%<%] !! * <

!! ### Boundaries

 %> # !!≈  **@CODE@** for suffix and word end

!! #### Morpheme boundaries:
 «  !!≈ * **@CODE@** = Derivational prefix
 »  !!≈ * **@CODE@** = Derivational suffix
 %< !!≈ * **@CODE@** = Inflectional prefix
 %> !!≈ * **@CODE@** = Inflectional suffix
 #  !!≈ * **@CODE@** = Word boundary for both lexicalised and dynamic compounds
 %^ !!≈ * **@CODE@** = (exceptional) soft hyphenation point
 %- !!≈ * **@CODE@** = Hyphen for question particle
 ð  !!≈ * **@CODE@** = Compounding rules

!! End of alphabet definitions
 ;

!! ### Sets
Sets

 Vow = а е и о у ы э ю я			   !!≈ * **@CODE@**
       А Е И О У Ы Э Ю Я			   !!≈  **@CODE@**
       а̄ е̄ ӣ о̄ ӯ ы̄ э̄ ю̄ я̄ ё̄	   !!≈  **@CODE@**
       А̄ Е̄ Ӣ О̄ Ӯ Ы̄ Э̄ Ю̄ Я̄ Ё̄ ;  !!≈  **@CODE@**
 VowNoI = а е   о у э ю я				   !!≈ * **@CODE@**
          А Е   О У Э Ю Я				   !!≈  **@CODE@**
          а̄ е̄ ӣ о̄ ӯ ы̄ э̄ ю̄ я̄ ё̄	   !!≈  **@CODE@**
          А̄ Е̄ Ӣ О̄ Ӯ Ы̄ Э̄ Ю̄ Я̄ Ё̄ ;   !!≈  **@CODE@**
  PalVow = е ё и ю я е̄ ӣ ю̄ я̄ ё̄ ;  !!≈ * **@CODE@**
SVow = а е ё и о у ы э ю я ; !!≈ * **@CODE@**
LVow = а̄ е̄ ё̄ ӣ о̄ ӯ ы̄ э̄ ю̄ я̄ ; !!≈ * **@CODE@**
 CnsSoft = щ Щ  ;  !!≈ * **@CODE@**
 CnsNonAlv = б в г ж к м п ф х ц ч ш ӈ   !!≈ * **@CODE@**
             Б В Г Ж К М П Ф Х Ц Ч Ш Ӈ  ; !!≈  **@CODE@**
 CnsNoReqFront = б в г к м п ф х ӈ   !!≈ * **@CODE@**
                 Б В Г К М П Ф Х Ӈ  ; !!≈  **@CODE@**
 CnsAlv = д з л н с т !!≈ * **@CODE@**
          Д З Л Н С Т ; !!≈  **@CODE@**
 Cns = CnsSoft CnsNonAlv CnsAlv  р Р й Й ;  !!≈ * **@CODE@**


!! ### Definitions
Definitions

Pal = [%{VCCS%}:|%{VCS%}:|%{SYNCS%}:|%{NOSYNCS%}:] ;


! PostSyncStem  =  :Cns  %+:0 ;
PostSyncStem  =  :Cns   ;  !!≈ * **@CODE@** 


!! ## Rules
Rules

"Syncope for ы"
!! **RULE: @RULENAME@**  
 ы:0 <=> Cns _ (Cns) [Cns - й] [%{SYNCH%}:|%{SYNCS%}:] %> [ы: [т|н|г|л|ӈ]|ы̄:|а|%{аяØ%}:|%{уюØ%}:|(%{тØ%}:) [%{ЭЕ%}:|%{ЭЕLong%}:]] ;
  %{VA%}: %> %{аяØ%}: %> _ ;
  [%{VI%}:]  %> _ [т|л] ; !Plural
  ы [%{VI%}:]  %> _ [н] ; ! PxSg2
  [%{VO%}:]  %> _ [г|т|л|н|м] ; !Pl, Tra, Du,PxPl2
 Cns _ (Cns) [Cns - й] [%{SYNCH%}:|%{SYNCS%}:] %>  %{ыиØ%}: т ; !Loc
  [э̄|а̄|ы|и] (%{VU%}:) %> _ ;

!ё х т ы {VU}:0 > ы:0 с

!! тэ̄ӈкве+V+Act+Ind+Prt+ScSg1+OcSg3: __eat/syödä__
!!€ тэ̄>ыслум
!!€ тэ̄>0слум
!!€ ловиньт{VA}>{аяØ}>ыс
!!€ ловиньт0>а>0с
!! павыл+N+Pl+Nom:
!!€ павыл{SYNCH}>ыт
!!€ пав0л0>ыт
!!
!!€ хӯрыг{SYNCH}>ыт
!! кол+N+Pl+Nom: **house/koti**
!!€ кол{VCH}>ыт
!!€ кол0>ыт
!! кол+N+Sg+Loc: **house/koti**
!!€ кол{VCH}>{ыиØ}т
!!€ кол0>0т
!! класс+N+Sg+Loc:
!!€ класс{VCCH}>{ыиØ}т
!!€ класс0>ыт
!! класс+N+Pl+Nom:
!!€ класс{VCCH}>ыт
!!€ класс0>ыт

"Syncope for и"
!! **RULE: @RULENAME@**  
 и:0 <=> Cns _ (Cns) [Cns - й] [%{SYNCH%}:|%{SYNCS%}:] %> [%{ыиØ%}:|а|%{аяØ%}:|%{уюØ%}:|%{ЭЕ%}:] ;

"Syncope for у"
!! **RULE: @RULENAME@**  
 у:0 <=> Cns _ (Cns) [Cns - й] [%{SYNCH%}:|%{SYNCS%}:] %> [%{ыиØ%}:| ы т|а|%{аяØ%}:|%{уюØ%}:|%{ЭЕ%}:] ;


!! ла̄тыӈ+N+Sg+PxPl1+Nom:  **language/kieli**
!!€ ла̄тыӈ{SYNCH}>{уюØ}в
!!€ ла̄т0ӈ0>ув
!! яныг+N+Sg+Loc:
!!€ яныг{SYNCH}>{ыиØ}т
!!€ ян0г0>ыт

!! vowel lengthening
"a becomes long"
!! **RULE: @RULENAME@**
 а:а̄ <=> _ %{VO%}: %> ы̄: ; 
     _ %{VO%}: %> %{аяLong%}: ;

"ja becomes long"
!! **RULE: @RULENAME@**
 я:я̄ <=> _ %{VO%}: %> ы̄: ; 
     _ %{VO%}: %> %{аяLong%}: ;

"y becomes long"
!! **RULE: @RULENAME@**
 ы:ы̄ <=> Cns _ %{VI%}: %> ы̄: ; 

"long y disappears"
!! **RULE: @RULENAME@**
 ы̄:0 <=>  [%{VO%}:|%{VI%}:] %> _ ;

!! #### Tests:
 
! !€ апа>{йØ}ыт
! !€ апа>00т
! ! аки+N+Du+Nom:  __paternal uncle younger than father__
! !€ аки>{йØ}ыг
! !€ аки>0иг
!!€ ха̄п>{йØ}ыт
!!€ ха̄п>0ыт
!!€ апа>ныл
!!€ апа>ныл
!!€ паль{VCS}>ныл
!!€ паль0>ныл
! !€ ха̄п>ыныл
! !€ ха̄п>0ныл  

!! #### Tests:

!!€ паль{VCS}>{йØ}{ыиØ}т
!!€ пал00>0ит

!! #### Tests:

!!€ а̄ны{VI}>{йØ}{ыиØ}г
!!€ а̄ны0>0иг
!! аки+N+Sg+Tra: __as an uncle__
!!€ аки{VI}>{йØ}{ыиØ}г
!!€ аки0>0иг

!%{ыиØ%}:0
"Vow ы in loc appears"
%{ыиØ%}:0 <=>
 [%{SYNCH%}:] %> _ [н] ;
  [%{SYNCS%}:|%{VO%}:] %> _ [т|н] ;

!%{ыиØ%}:ы this is for Loc
"Vow ы in loc appears"
%{ыиØ%}:ы <=>
     [%{SYNCH%}:] %> _ [т|л|я] ;
     [%{NOSYNCH%}:] %> _ [л|я] ;
     [%{VCH%}:|%{VCCH%}:] %> _ [т|н|л|я] ;
     [CnsAlv:] [%{VU%}:|%{VA%}:] %> _ [м|н] ;
!! павыл+N+Sg+Loc:
!!€ павыл{SYNCH}>{ыиØ}т
!!€ павыл0>0т
!!
!! я̄ӈк+N+Sg+Loc: **ice/jää**
!!€ я̄ӈк{VCCH}>{ыиØ}т
!!€ я̄ӈк0>ыт

"Vow и in loc appears"
%{ыиØ%}:и <=>
     [%{SYNCS%}:|%{NOSYNCS%}:] %> _ [л|я] ;
     [%{VCCS%}:|%{VCS%}:] %> _ [т|н|л|я] ;
     [CnsSoft | CnsNonAlv | р | Р | й: | Й:] %{VU%}: %> _ [м|н] ;
!!ансамбль+N+Sg+Loc: **band/yhtye**
!!€ ансамбль{VCCS}>{ыиØ}т
!!€ ансамбл00>ит
!!€ паль{VCS}>{ыи0}т
!!€ пал00>ит

"Vow и in plural ы to и"
ы:и <=>
 [ %{VCCS%}: | %{VCS%}: ] %> _ [т|н|г|л|с] ;
  [%{SYNCS%}:] %> _ [т|н|г|л] ;
  [%{VI%}:] %> _ [г] ;
  [CnsSoft | й: | Й:] [%{VA%}:|%{VU%}:] %> _ [м|с .#. ] ;
!  [CnsSoft | CnsNonAlv | р | Р | й: | Й:] [%{VA%}:|%{VU%}:] %> _ [м|с .#. ] ;
!!ансамбль+N+Pl+Nom: **band/yhtye**
!!€ ансамбль{VCCS}>ыт
!!€ ансамбл00>ит
!!€ паль{VCS}>ыт
!!€ пал00>ит

"long Vow ӣ in plural ы̄ to ӣ"
ы̄:ӣ <=>
 [ %{VCCS%}: | %{VCS%}: ] %> _ [т] ;
  [%{SYNCS%}:] %> _ [т|н|г] ;

!! #### Tests:
!ве̄рмуӈкве+Ind+Prs+OcPl3+ScSg3:
!!€ ве̄рмы%>%{ЫИ%}янэ
!!€ ве̄рм0%>иянэ

! +V+Ind+Prs+ScSg1:
! € >%{ЭЕLong%}гум

!! #### Tests:

!!€ вармаль{VCS}>{ыиØ}т
!!€ вармал00>ит
!!$ та̄л{VCS}>{ыиØ}г
!!$ та̄л0>иг


!! #### Tests:

!!€ са̄лы{VI}>{аяØ}гын
!!€ са̄лы0>ягын

!! #### Tests:

!!€ са̄лы{VI}>{уюØ}м
!!€ са̄лы0>0м
!!€ аки{VI}>{уюØ}м
!!€ аки0>0м



"Soft sign deletion for и suffixes"
!! **RULE: @RULENAME@**  2023.02
ь:0 <=>
    _ [%{VCCS%}:|%{VCS%}:|%{SYNCS%}:] %>  %{ыиØ%}: [т|н|л]; !! ансамбль+N+Sg+Loc: **band/yhtye**
    _ [%{VCCS%}:|%{VCS%}:|%{SYNCS%}:] %>  ы: [т|г|м]; !! ансамбль+N+Pl+Nom: **band/yhtye**
    _ [%{VCCS%}:|%{VCS%}:|%{SYNCS%}:] %> (%{йØ%}:0)  %{ыиØ%}: [г]; !! ансамбль+N+Sg+Loc: **band/yhtye**
!!€ ансамбль{VCCS}>{ыиØ}т
!!€ ансамбл00>ит
    _ [%{VU%}:|%{VA%}:|%{VCCS%}:|%{VCS%}:|%{SYNCS%}:] %> [%{уюØ%}:| %{аяØ%}: ]; !! XX+V+Inf: ****

 Vow н: _ щ %{VU%}: %> с ;

!Cns _ (ы:0) %> (%{йØ%}:0) [:PalVow| %{тØ%}: [ %{ЭЕ%}: | %{ЭЕLong%}: ]] ;
!above

!! #### Tests:

!! ха̄ль+N+Sg+Nom+PxSg3
!!€ ха̄ль{VCS}>{тØ}{ЭЕ}
!!€ ха̄л00>0е

!!€ паль{VCS}>{йØ}{ыиØ}г
!!€ пал00>0иг
! In light of the yaml file saali, this seems wrong, we want са̄лыиг, нот са̄лыйыг
!"%{йØ%}:й instertion for vowel initial suffixes on ы final stems"
!%{йØ%}:й <=> [ы|и] %> _ ы г ;
!€ са̄лы{VI}>0{ыиØ}г
!€ са̄лы0>йыг

!!TESTS
!echo 'л а̄ т {ыØ}:0 ӈ > {уюØ}:у в' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'х а̄ л ь:0 > {тØ}:0 {ЭЕ}:е' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'я н ы > {тØ}:т ы л' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'о к р у г > {уюØ}:у в' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'ла̄тыӈ+N+Sg+Nom' | hfst-lookup src/morphology/lexicon.hfst

!! павыл+N+Pl+Nom:
!!€ павыл{SYNCH}>ыт
!!€ пав0л0>ыт



"%{аяØ%}:0"
!! __@RULENAME@__ deletes а/я after VO vowel stems
%{аяØ%}:0 <=> %{VO%}:  %> _  ;
!VowNoI  %> _  ;


"%{аяØ%}:а"
!! __@RULENAME@__
 %{аяØ%}:а <=> [%{SYNCH%}: | %{NOSYNCH%}: | %{VCH%}: | щ %{VCS%}:| щ %{SYNCS%}:| %{VCCH%}: | [CnsSoft | CnsAlv | CnsNonAlv| р] [%{VA%}:|%{VU%}:]]  %> _  ;
! Cns %> _  ;
!<=-rule conflict between "{аяØ}:а" and "{аяØ}:я".
!E.g. in context __HFST_TWOLC_.#.:__HFST_TWOLC_.#. ӈ:ӈ ь:ь щ:щ {VCS}: >:> _ __HFST_TWOLC_.#.:__HFST_TWOLC_.#.

"%{аяØ%}:я"
!! __@RULENAME@__
  %{аяØ%}:я <=> [%{VI%}: | [ь: | й:] Pal | [ь: | й:] [%{VA%}:|%{VU%}:]]  %> _  ;
                %{VU%}: %> %{ЫИ%}:ы %> _ ;
!  п а т т {VU}:0 > {ЫИ}:ы > {аяØ}:я н у в
  ! [ и | ы | ь: ] %> _  ;


"%{уюØ%}:0"
!! __@RULENAME@__ deletes а/я after VO vowel stems
%{уюØ%}:0 <=> [%{VO%}:]  %> _  ;
            [%{VI%}:]  %> _ м ;
!VowNoI  %> _  ;


"%{уюØ%}:у"
!! __@RULENAME@__
 %{уюØ%}:у <=> [%{SYNCH%}: | %{NOSYNCH%}: | %{VCH%}: | %{VCCH%}: | [CnsSoft | CnsAlv| CnsNonAlv | р] %{VU%}: ]  %> _  ;
    [ы:0|ы] щ %{SYNCS%}: %> _  Cns ; 
! Cns %> _  ;
!! ща̄рыщ+N+Sg+Nom+PxSg1: __sea/meri__
!!€ ща̄рыщ{SYNCS}>{уюØ}м
!!€ ща̄р0щ0>юм

"%{уюØ%}:ю"
!! __@RULENAME@__
  %{уюØ%}:ю <=> [ %{VCCS%}:|%{VCS%}:|%{NOSYNCS%}: | [й:| ь: ] %{VU%}:]  %> _  ;
                                   %{VI%}:  %> _  в ;
                       CnsAlv ь: %{SYNCS%}: %> _ Cns ; 
  ! [ и | ы | ь: ] %> _  ;
!! коюӈкве+V+Inf: **track/jäljittää, vuottaa**
!!€ кой{VU}>{уюØ}ӈкве
!!€ ко00>юӈкве

"%{аяLong%}:а̄"
!! __@RULENAME@__
%{аяLong%}:а̄ <=>  [ Cns (%{SYNCH%}:|%{NOSYNCH%}:|%{VCH%}:|%{VCCH%}:)| г] %> _ ;
!   г %> _ ;
!коскер+N+Sg+Nom+PxPl3+Err/Orth:

"%{аяLong%}:я̄"
!! __@RULENAME@__
%{аяLong%}:я̄ <=>   %{ЫИ%}: %> _ ;
!+Act+Ind+Prs+ScPl3+OcSg3+Err/Orth

"%{аяLong%}:0"
!! __@RULENAME@__
%{аяLong%}:0 <=> [ я: | а: ]  %{VO%}: %> _ ;




"%{ЭЕLong%}:э̄"
!! __@RULENAME@__
%{ЭЕLong%}:э̄ <=>     [CnsAlv %{VA%}:|%{VO%}:]   %> (%{тØ%}:) _ ;
             [CnsAlv - р|Vow] [%{VU%}:|%{VA%}:] %>           _ ; ! from "%{ЭЕ%}:э"
!! аки
!!€ аки{VI}>%{тØ%}%{ЭЕLong%}н
!!€ аки0>тэ̄н
                   CnsAlv [%{VA%}:|%{VU%}:] %> (%{тØ%}:) _ [г | в] ;
 [ CnsAlv ] [%{SYNCH%}:|%{VCH%}:|%{VCCH%}:] %> (%{тØ%}:) _ ;

 "%{ЭЕLong%}:е̄"
 !! __@RULENAME@__
  %{ЭЕLong%}:е̄ <=> [ CnsSoft | CnsNonAlv | ь: | й: | р] [%{VA%}:|%{VU%}:] %> _ ;
 !!€ ха̄ль%>%{тØ%}%{ЭLongЕ%н
 !!€ ха̄л0%>0е̄н
 а̄ %> _ ;
 [ CnsSoft | CnsNonAlv | ь: | й: | р] [%{VA%}:|%{VU%}:] %> _ [г | в ] ;
    CnsNonAlv [ %{VCH%}:|%{VCCH%}:] %> _ ;
  [ CnsSoft | CnsNonAlv | ь: | й: | р] %{SYNCH%}: %> (%{тØ%}:) _ ;
   %{SYNCS%}: %> (%{тØ%}:) _ ;
 !!€ хӯрыг{SYNCH}>{ЭЕLong}н
 !!€ хӯр0г0>е̄н

!2023_02_10fixme!"%{ЭЕLong%}:э"
!2023_02_10fixme!!! __@RULENAME@__
!2023_02_10fixme! %{ЭЕLong%}:э <=> CnsAlv %{VU%}: %> _ ;
!2023_02_10fixme! [[CnsAlv - р] %{VCH%}:| [CnsAlv - р] %{VCCH%}:| %{SYNCH%}:] %> (%{тØ%}:) _  ;
!2023_02_10fixme! !!€ тот{VU}>{ЭЕLong}в
!2023_02_10fixme! !!€ тот0>эв
!2023_02_10fixme!
!2023_02_10fixme! "%{ЭЕLong%}:е"
!2023_02_10fixme! !! __@RULENAME@__
!2023_02_10fixme!  %{ЭЕLong%}:е <=> [ CnsSoft | CnsNonAlv | ь: | й: ] %{VU%}: %> _ ;
!2023_02_10fixme! [[р|CnsNonAlv] [%{VCH%}:|%{VCCH%}:| %{SYNCH%}:]|Pal] %> (%{тØ%}:) _  ;
!2023_02_10fixme! !!€ ха̄ль%>%{тØ%}%{ЭLongЕ%н
!2023_02_10fixme! !!€ ха̄л0%>0е̄н

"{ЭЕ}:э"
!! __@RULENAME@__
%{ЭЕ%}:э <=> [CnsAlv - р|Vow] [%{VU%}:|%{VA%}:] %>           _ ;
                         Vow  [ %{VO%}: | %{VI%}: ]          %>  %{тØ%}:  _ ;
  [CnsAlv - р] [%{VCH%}:| %{VCCH%}:|%{SYNCH%}:] %> (%{тØ%}:) _ ;
!!€ тот{VU}>{ЭЕ}м
!!€ тот0>эм

"{ЭЕ}:е"
!! __@RULENAME@__
 %{ЭЕ%}:е <=> [ CnsSoft | р | CnsNonAlv | ь: | й:] [%{VU%}:|%{VA%}:] %>  _  ;
 [ [р| CnsNonAlv] [%{VCH%}:|%{VCCH%}:|%{SYNCH%}:] |Pal| %{SYNCS%}:] %> (%{тØ%}:) _  ;
             	     а̄ %>  _ ;
!!€ ха̄ль%>%{тØ%}%{ЭЕ%}
!!€ ха̄л0%>0е
!!€ потыр%^Syncope%>%{тØ%}%{ЭЕ%}
!!€ пот0р0%>0е

"%{ЫИ%}:ы"
!! __@RULENAME@__
%{ЫИ%}:ы <=> [ CnsAlv - р | Vow ] [%{VU%}:|%{VA%}:] %> _ ;
!! тотуӈкве+V+Ind+Prs+ScDu1+OcDu3:
!!€ тот{VU}>{ЫИ}ягмен
!!€ тот0>ыягмен
!! tотуӈкве+V+Ind+Prs+ScPl1+OcSg3:
!!€ тот{VU}>{ЫИ}лӯв
!!€ тот0>ылӯв
!lang-mns jackrueter$ echo 'т о т {VU}:0 > {ЫИ}:ы л ӯ в'| hfst-pair-test src/fst/phonology.compose.hfst

"%{ЫИ%}:и"
!! __@RULENAME@__
 %{ЫИ%}:и <=> [ CnsSoft | р | CnsNonAlv | ь: | й: ] [%{VU%}:|%{VA%}:] %>  _  ;

!! ### CONSONANTS
"й:0"
!! __@RULENAME@__
 й:0 <=> _ [%{VCS%}:| %{VU%}: | %{VA%}: ] %> [%{ЫИ%}:|%{ыиØ%}:|%{ЭЕ%}:|%{ЭЕLong%}:| ы: |%{уюØ%}:|%{аяØ%}:] ; 
!! коюӈкве+V+Inf: ****
!!€ кой{VU}>{уюØ}ӈкве
!!€ ко00>юӈкве

"%{тØ%}:0"
! __@RULENAME@__
 %{тØ%}:0 <=> Cns (ь: ) [%{SYNCH%}:|%{SYNCS%}:|%{VCS%}:|%{VCH%}:|%{VCCS%}:|%{VCCH%}:] %> _ [ %{ЭЕ%}: | %{ЭЕLong%}: | ы: л ]  ;

! # Cns* (Vow Cns) Vow Cns (ь: ) %> _ [ %{ЭЕ%}: | %{ЭЕLong%}: ]  ;
!! ха̄ль+N+Sg+PxSg3+Nom
!!€ ха̄ль%>%{тØ%}%{ЭЕ%}
!!€ ха̄л0%>0е
!! кол+N+Sg+Ins:
!!€ кол%>%{тØ%}ыл
!!€ кол%>0ыл
!! яныт+N+Sg+Ins:
!!€ яныт>{тØ}ыл
!!€ яныт>0ыл
!! потыр+N+Sg+PxSg3+Nom
!!€ потыр>{тØ}{ЭЕ}
!!€ пот0р>0е
! VowNoI %>  _ ы: л ;
!! э̄ква+N+Sg+Ins
!!€ э̄ква>{тØ}ыл
!!€ э̄ква>00л


"%{тØ%}:т"
! __@RULENAME@__
%{тØ%}:т <=> Vow [ %{VO%}: | %{VI%}: ] %> _ [ %{ЭЕ%}: | %{ЭЕLong%}: ]  ;
            ы  %{VI%}: %> _ %{ыиØ%}: л ;

"н:0 in verbs"
! __@RULENAME@__
н:0 <=>  Vow _ [с| ь: щ] %{VU%}: %> [с|н э | в е|в е̄| м а] ;
!! хансуӈкве+V+PrsPrc: __write/kirjoittaa__
!!€ ханс{VU}>нэ
!!€ ха0с0>нэ

"ӈ:0 in verbs"
! __@RULENAME@__
ӈ:0 <=>  Vow _ [ х ] %{VU%}: %> [с|н э | в е|в е̄| м а] ;
!! таӈхуӈкве+V+Act+Ind+Prt+ScSg1: ____
!!€ таӈх{VU}>сум
!!€ та0х0>сум

!€ a
!€ b

!TOS !!€ потыр%^Syncope%>%{УЮØ%}м
!TOS !!€ пот0р0%>ум
!TOS !!€ потыр%^Syncope%>%{тØ%}%{ЭЕ%}
!TOS !!€ пот0р0%>0е
!TOS !!€ потыр%^Syncope%>%{ЫИØ%}т
!TOS !!€ пот0р0%>ыт
!TOS !!€ потыр%^Syncope%>0ме̄н
!TOS !!€ пот0р0%>уме̄н

!TOS !!€ э̄ква%>%{тØ%}ыл
!TOS !!€ э̄ква%>00л


!TOS !!€ па̄рт%>%{ЫИØ%}г%>%{ыØ%}н
!TOS !!€ па̄рт%>ыг%>0н
!TOS !!€ ха̄п%>%{ыØ%}н
!TOS !!€ ха̄п%>0н

!TOS !!€ па̄рт%>%{ыØ%}н
!TOS !!€ па̄рт%>ын
!TOS !!€ потыр%^Syncope%>%{ыØ%}н
!TOS !!€ пот0р0%>ын

!TOS !!€ рума%>%{ЫИØ%}%^PxPl2н
!TOS !!€ рума̄%>00н

!TOS !!€ потыр%>%{уюØ%}ме̄н
!TOS !!€ пот0р%>уме̄н

