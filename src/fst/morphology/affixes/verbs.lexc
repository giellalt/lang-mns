!! # Verb inflection for Mansi
! ---------------






LEXICON VAANGKVE
!! put derivations here
 V0_VAANGKVE ;
 
LEXICON V0_VAANGKVE
: INF ;
:г IND_PRS_TEENGKVE ;
: ACT_IND_PRT_SC_VA ;
!20230901: ACT_IND_PRT_SCSG2_sn ;
!20230901: ACT_IND_PRT_SCSG2_snyn ;
:г%{VU%} ACT_IND_PRS_OCSG_lumeen ;
:г%{VU%} ACT_IND_PRS_OCDU ;
:г%{VU%} ACT_IND_PRS_OCPL ;
: ACT_IND_PRT_OCSG_slumeen ;
: ACT_IND_PRT_OCDU ;
: ACT_IND_PRT_OCPL ;
: ACT_COND_VU ;
: ACT_IMPRT ;
: V11_MUTUAL_PASS_PRS ;
:й PASS_PRT ;
: PASS_COND ;
:г PASS_IMPRT ;
:и GER ;
:г VABESS ;
: PRSPRC ;
: PRFPRC ;


LEXICON TEENGKVE !тэ̄ӈкве+V:т
!! put derivations here
 V0_TEENGKVE ;
LEXICON V0_TEENGKVE !тэ̄ӈкве+V:т
:э̄ INF ;
:э̄г IND_PRS_TEENGKVE ;
:э̄ ACT_IND_PRT_SC_VA ;
:э̄ ACT_IND_PRT_SCSG2_sn ;
:э̄ ACT_IND_PRT_SCSG2_snyn ;
:э̄г%{VU%} ACT_IND_PRS_OCSG_lumeen ;
:э̄г%{VU%} ACT_IND_PRS_OCDU ;
+Act+Ind+Prs+ScSg2+OcDu:э̄г%{VU%}%>%{аяØ%}гн VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR V-6-tengkwe
+Act+Ind+Prs+ScSg2+OcDu:э̄г%{VU%}%>%{аяØ%}гнын VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR V-6-tengkwe
+Act+Ind+Prs+ScDu1+OcDu:э̄г%{VU%}%>%{аяØ%}гаме̄н VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR V-6-tengkwe
+Act+Ind+Prs+ScDu2+OcDu:э̄г%{VU%}%>%{аяØ%}гагы̄н VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR V-6-tengkwe
+Act+Ind+Prs+ScDu3+OcDu:э̄г%{VU%}%>%{аяØ%}гаге̄н VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR V-6-tengkwe

:э̄г%{VU%} ACT_IND_PRS_OCPL ;
+Act+Ind+Prs+ScDu1+OcPl:э̄г%{VU%}%>%{аяØ%}нанме̄н VERB_K ;!Jaska 20230901
+Act+Ind+Prs+ScDu3+OcPl:э̄г%{VU%}%>%{аяØ%}нанэ̄н VERB_K ;!Jaska 20230901
+Act+Ind+Prs+ScPl1+OcPl:э̄г%{VU%}%>%{аяØ%}нанув VERB_K ;!Jaska 20230901
+Act+Ind+Prs+ScPl1+OcPl:э̄г%{VU%}%>%{аяØ%}нанӯв VERB_K ;!Jaska 20230901
:э̄ ACT_IND_PRT_OCSG_slumeen ;
:э̄ ACT_IND_PRT_OCDU ;
:э̄ ACT_IND_PRT_OCPL ;
:э̄ ACT_COND_VU ;
:а̄ ACT_IMPRT ;
:э̄ V11_MUTUAL_PASS_PRS ;
:а̄й PASS_PRT ;
:э̄ PASS_COND ;
:э̄г PASS_IMPRT_teegveen ;
:а̄и GER ;
:э̄г VABESS ;
:э̄ PRSPRC ;
:э̄ PRFPRC_teemanum ;


LEXICON JINGKVE
!! put derivations here
 V0_JINGKVE ;
LEXICON V0_JINGKVE
:йи INF ; !!≈ @CODE@
:йи PRSPRC_jingkve ;
+Act+Ind+Prs+ScSg1:ювум VERB_K ; !мыгӯв?
+Act+Ind+Prs+ScSg2:ювын VERB_K ;  !мае̄н? 
+Act+Ind+Prs+ScSg3:юв VERB_K ;  !мае̄н? 
+Act+Ind+Prs+ScDu1:ювме̄н VERB_K ;
+Act+Ind+Prs+ScDu2:ювын VERB_K ;
+Act+Ind+Prs+ScDu2:ювы̄н VERB_K ;
+Act+Ind+Prs+ScDu3:ювыг VERB_K ;
+Act+Ind+Prs+ScPl1:ювув VERB_K ;
+Act+Ind+Prs+ScPl1:ювӯв VERB_K ;
+Act+Ind+Prs+ScPl2:ювын VERB_K ;
+Act+Ind+Prs+ScPl2:ювы̄н VERB_K ;
+Act+Ind+Prs+ScPl3:ювыт VERB_K ;
:йи ACT_IND_PRT_SCSG1_sum ;
:йи ACT_IND_PRT_SCSG2_syn ;
:йи ACT_IND_PRT_SCSG3_ys ;
:йи ACT_IND_PRT_SCDU1_sumeen ;
:йи ACT_IND_PRT_SCDU2_syn ;
:йи ACT_IND_PRT_SCDU2_syyn ;
:йи ACT_IND_PRT_SCDU3_syg ;
:йи ACT_IND_PRT_SCDU3_syyg ;
:йи ACT_IND_PRT_SCPL1_suv ;
:йи ACT_IND_PRT_SCPL1_suuv ;
:йи ACT_IND_PRT_SCPL2_syn ;
:йи ACT_IND_PRT_SCPL2_syyn ;
:йи ACT_IND_PRT_SCPL3_syt ;

!Cond
:йи ACT_COND_VU ;
:яй%{VU%} ACT_IMPRT ;

:йи V11_MUTUAL_PASS_PRS ;
:яй PASS_PRT ;
!+Pass+Ind+Prt+ScPl1:яй%>весӯв VERB_K ;!янытлавесӯв Jaska 20230901, AGAINST V-6-lingkwe, tengkwe, wingkwe, mingkwe ;FOR V-6-jingkwe
:йи PASS_COND ;
:йи PASS_IMPRT ;
:йи PRFPRC ;
:йийи GER ;
:йим VABESS ;

LEXICON LYNGKVE
!! put derivations here
 V0_LYNGKVE ;
LEXICON V0_LYNGKVE
:лы INF ; !!≈ @CODE@
:лы PRSPRC ;
+Act+Ind+Prs+ScSg1:лыгум VERB_K ; !мыгӯв?
!+Act+Ind+Prs+ScSg2:лаен VERB_K ;  !мае̄н? 
+Act+Ind+Prs+ScSg2:лыгын VERB_K ;
+Act+Ind+Prs+ScSg3:лыг VERB_K ;  !мае̄н? 
+Act+Ind+Prs+ScDu1:лыгме̄н VERB_K ;
!+Act+Ind+Prs+ScDu2:лае̄н VERB_K ;
+Act+Ind+Prs+ScDu2:лыгын VERB_K ;
+Act+Ind+Prs+ScDu2:лыгы̄н VERB_K ;
+Act+Ind+Prs+ScDu3:лыгыг VERB_K ;
+Act+Ind+Prs+ScPl1:лыгӯв VERB_K ;
+Act+Ind+Prs+ScPl1:лыгув VERB_K ;
!+Act+Ind+Prs+ScPl2:лае̄н VERB_K ;
+Act+Ind+Prs+ScPl2:лыгын VERB_K ;
+Act+Ind+Prs+ScPl2:лыгы̄н VERB_K ;
+Act+Ind+Prs+ScPl3:лыгыт VERB_K ;

:лы ACT_IND_PRT_SC_VA ;
!:лы ACT_IND_PRT_SCSG1_sum ;
!:лы ACT_IND_PRT_SCSG2_syn ;
!:лы ACT_IND_PRT_SCSG3_ys ;
!:лы ACT_IND_PRT_SCDU1_ysmeen ;
!:лы ACT_IND_PRT_SCDU2_syyn ;
!:лы ACT_IND_PRT_SCDU3_syyg ;
!:лы ACT_IND_PRT_SCPL1_suv ;
!:лы ACT_IND_PRT_SCPL1_suuv ;
!:лы ACT_IND_PRT_SCPL2_syyn ;
!:лы ACT_IND_PRT_SCPL3_syt ;

:лыг%{VU%} ACT_IND_PRS_OCSG_lumeen ;
:лыг%{VU%} ACT_IND_PRS_OCDU ;
:лыг%{VU%} ACT_IND_PRS_OCPL ;
:лы ACT_IND_PRT_OCSG_slumeen ;
:лы ACT_IND_PRT_OCDU ;
:лы ACT_IND_PRT_OCPL ;

!Cond
:лы ACT_COND_VU ;
:лай%{VU%} ACT_IMPRT_VU ;
+Act+Imprt+ScDu2+OcPl:лай%{VU%}%>%{ЭЕLong%}нын VERB_K ;! Jaska 20230901 AGAINST V-6-tengkwe ; FOR V-6-mingkwe, wingkwe, V-6-lingkwe
+Act+Imprt+ScPl2+OcPl:лай%{VU%}%>%{ЭЕLong%}нын VERB_K ;! Jaska 20230901 AGAINST V-6-tengkwe ; FOR V-6-mingkwe, wingkwe, V-6-lingkwe

:лы V11_MUTUAL_PASS_PRS ;

:лай PASS_PRT ;
:лы PASS_COND ;
:лыг PASS_IMPRT_teegveen ;
:лы PRFPRC_teemanum ;!Will need more 20230622
:ла̄и GER ;
:лыг VABESS ;

LEXICON MINGKVE
!! put derivations here
 V0_MINGKVE ;
LEXICON V0_MINGKVE
:ми INF ; !!≈ @CODE@
:ми PRSPRC ;
+Act+Ind+Prs+ScSg1:мыгум VERB_K ; !мыгӯв?
+Act+Ind+Prs+ScSg2:мыгын VERB_K ;  !мае̄н? 
+Act+Ind+Prs+ScSg3:мыг VERB_K ;  !мае̄н? 
+Act+Ind+Prs+ScDu1:мыгме̄н VERB_K ;
+Act+Ind+Prs+ScDu2:мыгы̄н VERB_K ;!Jaska 20230901, V-6-mingkwe
+Act+Ind+Prs+ScDu2:мыгын VERB_K ;!Jaska 20230901, V-6-mingkwe
+Act+Ind+Prs+ScDu3:мыгыг VERB_K ;
+Act+Ind+Prs+ScPl1:мыгӯв VERB_K ;
+Act+Ind+Prs+ScPl1:мыгув VERB_K ;!Jaska 20230901, V-6-mingkwe
+Act+Ind+Prs+ScPl2:мыгы̄н VERB_K ;!Jaska 20230901, V-6-mingkwe
+Act+Ind+Prs+ScPl2:мыгын VERB_K ;!Jaska 20230901, V-6-mingkwe
+Act+Ind+Prs+ScPl3:мыгыт VERB_K ;

:ми ACT_IND_PRT_SCSG1_sum ;
:ми ACT_IND_PRT_SCSG2_syn ;
:ми ACT_IND_PRT_SCSG3_ys ;
:ми ACT_IND_PRT_SCDU1_ysmeen ;
:ми ACT_IND_PRT_SCDU2_syyn ;
:ми ACT_IND_PRT_SCDU3_syyg ;
:ми ACT_IND_PRT_SCPL1_suv ;
:ми ACT_IND_PRT_SCPL2_syyn ;
:ми ACT_IND_PRT_SCPL3_syt ;

:ми ACT_IND_PRT_SC_VA ;

:мыг%{VU%} ACT_IND_PRS_OCSG_lumeen ;
:мыг%{VU%} ACT_IND_PRS_OCDU ;
:мыг%{VU%} ACT_IND_PRS_OCPL ;
:ми ACT_IND_PRT_OCSG_slumeen ;
:ми ACT_IND_PRT_OCDU ;
:ми ACT_IND_PRT_OCPL ;

!Cond
:ми ACT_COND_VU ;
:май%{VU%} ACT_IMPRT_VU ;
+Act+Imprt+ScDu2+OcPl:май%{VU%}%>%{ЭЕLong%}нын VERB_K ;! Jaska 20230901 AGAINST V-6-tengkwe ; FOR V-6-mingkwe, wingkwe, V-6-lingkwe
+Act+Imprt+ScPl2+OcPl:май%{VU%}%>%{ЭЕLong%}нын VERB_K ;! Jaska 20230901 AGAINST V-6-tengkwe ; FOR V-6-mingkwe, wingkwe, V-6-lingkwe
:ми V11_MUTUAL_PASS_PRS ;

:май PASS_PRT ;
:ми PASS_COND ;
:мыг PASS_IMPRT_teegveen ;
:ми PRFPRC_teemanum ;!Will need more 20230622
:ма̄и GER ;
:мыг VABESS ;


LEXICON VINGKVE
!! put derivations here
 V0_VINGKVE ;
LEXICON V0_VINGKVE
:ви INF ; !!≈ @CODE@
:ви PRSPRC ;
+Act+Ind+Prs+ScSg1:выгум VERB_K ; !мыгӯв?
+Act+Ind+Prs+ScSg2:выгын VERB_K ;  !мае̄н? 
+Act+Ind+Prs+ScSg3:выг VERB_K ;  !мае̄н? 
+Act+Ind+Prs+ScDu1:выгме̄н VERB_K ;
+Act+Ind+Prs+ScDu2:выгы̄н VERB_K ;
+Act+Ind+Prs+ScDu2:выгын VERB_K ;!Jaska 20230901, V-6-wingkwe
+Act+Ind+Prs+ScDu3:выгыг VERB_K ;
+Act+Ind+Prs+ScPl1:выгӯв VERB_K ;
+Act+Ind+Prs+ScPl1:выгув VERB_K ;
+Act+Ind+Prs+ScPl2:выгы̄н VERB_K ;
+Act+Ind+Prs+ScPl2:выгын VERB_K ;!Jaska 20230901, V-6-wingkwe
+Act+Ind+Prs+ScPl3:выгыт VERB_K ;

:ви ACT_IND_PRT_SCSG1_sum ;
:ви ACT_IND_PRT_SCSG2_syn ;
:ви ACT_IND_PRT_SCSG3_ys ;
:ви ACT_IND_PRT_SCDU1_ysmeen ;
:ви ACT_IND_PRT_SCDU2_syyn ;
:ви ACT_IND_PRT_SCDU3_syyg ;
:ви ACT_IND_PRT_SCPL1_suv ;
:ви ACT_IND_PRT_SCPL1_suuv ;
:ви ACT_IND_PRT_SCPL2_syyn ;
:ви ACT_IND_PRT_SCPL3_syt ;



:ви ACT_IND_PRT_SC_VA ;

:выг%{VU%} ACT_IND_PRS_OCSG_lumeen ;
:выг%{VU%} ACT_IND_PRS_OCDU ;
:выг%{VU%} ACT_IND_PRS_OCPL ;
:ви ACT_IND_PRT_OCSG_slumeen ;
:ви ACT_IND_PRT_OCDU ;
:ви ACT_IND_PRT_OCPL ;

!Cond
:ви ACT_COND_VU ;
:вой%{VU%} ACT_IMPRT_VU ;
+Act+Imprt+ScDu2+OcPl:вой%{VU%}%>%{ЭЕLong%}нын VERB_K ;!Jaska 20230901, AGAINST V-6-tengkwe ; FOR V-6-mingkwe, wingkwe, V-6-lingkwe
+Act+Imprt+ScPl2+OcPl:вой%{VU%}%>%{ЭЕLong%}нын VERB_K ;!Jaska 20230901, AGAINST V-6-tengkwe ; FOR V-6-mingkwe, wingkwe, V-6-lingkwe

:ви V11_MUTUAL_PASS_PRS ;

:вой PASS_PRT ;! 
+Use/NG:вуй PASS_PRT ;!
:ви PASS_COND ;
:выг PASS_IMPRT_teegveen ;
:ви PRFPRC_teemanum ;!Will need more 20230622
:вои GER ;
:выг VABESS ;



LEXICON MINUNGKVE
!! put derivations here
+Der+Der/Vlt+V:%>алт V0_A ;
+Der+Der/Vnt+V:%>ант V0_A ;
+Der+Der/Vasa+V:%>ас V0_A ;!2024-04-08
+Der+Der/Vyglal+V:%>ыгла̄л V0_U ;
+Der+Der/Vyglal+Err/Orth+V:%>ыглал V0_U ;
+Der+Der/Vyschl:%>ыщл V0_A ;
 V0_MINUNGKVE ;
 
LEXICON V0_MINUNGKVE
:у INF ; !!≈ @CODE@
 PRSPRC ;
+PrsPrc:%{VU%}%>%{ыиØ%}%>н VERB_K ; !ханищтахтын
+PrsPrc:%>%{аяØ%}%>н VERB_K ; !ханищтахтан
:%{VU%}%>%{ыиØ%} ACTIO ;
:%>%{аяØ%} PRFPRC ;

:%{VU%}%>%{ыиØ%} GER ;
:%{VU%} ACT_IND_PRS_VU ;

+Act+Ind+Prt+ScSg1:асум VERB_K ;
+Act+Ind+Prt+ScSg2:асын VERB_K ;
+Act+Ind+Prt+ScSg3:ас VERB_K ;
+Act+Ind+Prt+ScDu1:асаме̄н VERB_K ;
+Act+Ind+Prt+ScDu2:асы̄н VERB_K ;
+Act+Ind+Prt+ScDu2:асын VERB_K ;
+Act+Ind+Prt+ScDu3:асы̄г VERB_K ;
+Act+Ind+Prt+ScDu3:асыг VERB_K ;
+Act+Ind+Prt+ScPl1:асув VERB_K ;
+Act+Ind+Prt+ScPl1:асӯв VERB_K ;
+Act+Ind+Prt+ScPl2:асы̄н VERB_K ;
+Act+Ind+Prt+ScPl2:асын VERB_K ;
+Act+Ind+Prt+ScPl3:асыт VERB_K ;

: V11_MUTUAL_PASS ;

LEXICON JOXTUNGKVE
!! put derivations here
+Der+Der/Vlt+V:%>алт V0_A ;
+Der+Der/Vnt+V:%>ант V0_A ;
+Der+Der/Vasa+V:%>ас V0_A ;!2024-04-08
+Der+Der/Vyglal+V:%>ыгла̄л V0_U ;
+Der+Der/Vyglal+Err/Orth+V:%>ыглал V0_U ;
+Der+Der/Vyschl:%>ыщл V0_A ;
 V0_JOXTUNGKVE ;
LEXICON V0_JOXTUNGKVE
!! **LEXICON @LEXNAME@** 
:%{VU%}%>%{уюØ%} INF ;
:%{VU%}ы PRSPRC ; !cf U2C %{VU%} see V0_Y/U
!:%{VU%} V11_MUTUAL ;
:%{VU%}%>%{уюØ%} PRFPRC ;
:%{VU%}%>%{ыиØ%} GER ;!+Actio
:%{VU%}%>%{ыиØ%} ACTIO ;
+Der+Der/Ap+N:%{VU%}%>ап N_VCH ;
:%{VU%} ACT_IND_PRS_VU ;
:ы%{VU%} ACT_IND_PRT_OCDU/PL_VU ;!cf U2C with %{VU%}
:%{VU%} ACT_IMPRT_VU ;!all
:%{VU%} ACT_COND_VU ;!all

:ы%{VU%} ACT_IND_PRT_SC_VU ; ! ёхтысум!cf U2C %{VU%}
:ы%{VU%} ACT_IND_PRT_OCSG_slameen ; ! ёхтысаныл the plural is specialCHECKME !cf U2C %{VU%}
:%{VU%}у PASS_PRT ;!cf U2C %{VU%}
:%{VU%}%>%{аяØ%} PASS_PRS_VU ;
:%{VU%} PASS_COND ;


LEXICON IND_PRS_TEENGKVE
+Act+Ind+Prs+ScSg1:%>ум VERB_K ;
+Act+Ind+Prs+ScSg2:%>ын VERB_K ;
!+Act+Ind+Prs+ScSg2:%>нын VERB_K ;
+Act+Ind+Prs+ScSg3: VERB_K ;
+Act+Ind+Prs+ScDu1:%>ме̄н VERB_K ;
+Act+Ind+Prs+ScDu2:%>ын VERB_K ;
+Act+Ind+Prs+ScDu2+Use/NG:%>ы̄н VERB_K ;!2017: 134
+Act+Ind+Prs+ScDu3:%>ыг VERB_K ;
+Act+Ind+Prs+ScDu3+Err/Orth:%>ы̄г VERB_K ;!2017: 134, but Jaska 20230901, AGAINST tengkwe
+Act+Ind+Prs+ScPl1:%>ув VERB_K ;
+Act+Ind+Prs+ScPl1:%>ӯв VERB_K ;
+Act+Ind+Prs+ScPl2:%>ын VERB_K ;
+Act+Ind+Prs+ScPl2+Use/NG:%>ы̄н VERB_K ;!2017: 134
 ACT_IND_PRS_SCPL3_yt ;

!! ## Stems ending in C
LEXICON V_NSHU   !!≈ @CODE@ йинуӈкве
 V_U2C ;!2023-11-18

LEXICON V_NSSU  !!≈ @CODE@ о̄ньщуӈкве
 V_U2C ;!2023-11-18

LEXICON V_U !!≈ @CODE@ 
!! put derivations here
+Der+Der/Vlt+V:%>алт V0_A ;
+Der+Der/Vnt+V:%>ант V0_A ;
+Der+Der/Vasa+V:%>ас V0_A ;!2024-04-08
+Der+Der/Vyglal+V:%>ыгла̄л V0_U ;
+Der+Der/Vyglal+Err/Orth+V:%>ыглал V0_U ;
+Der+Der/Vyschl:%>ыщл V0_A ;
 V0_U ;
LEXICON V0_U !!≈ @CODE@ 
!! **LEXICON @LEXNAME@** 
:%{VU%}%>%{уюØ%} INF ;
:%{VU%} PRSPRC ; ! This will need some variants with onset vowel.
!:%{VU%} V11_MUTUAL ;
:%{VU%}%>%{уюØ%} PRFPRC_PLAIN ;
:%{VU%} PRFPRC_PERSON ;
+PrfPrc:%{VU%}%>%{ЫИ%}%>м # ;!CheckMe diff tag
:%{VU%}%>%{ыиØ%} GER ;!+Ger?+Actio
:%{VU%}%>%{ыиØ%} ACTIO ;
+Der+Der/Ap+N:%{VU%}%>ап N_VCH ;
:%{VU%} ACT_IND_VU ;
:%{VU%} ACT_IMPRT_VU ;!all
:%{VU%} ACT_COND_VU ;!all

:%{VU%} ACT_IND_PRT_SC_VU ;
:%{VU%} ACT_IND_PRT_OCSG_slameen ;
:%{VU%} PASS_PRT ;
:%{VU%}%>%{аяØ%} PASS_PRS_VU ;
+Err/Orth:%{VU%}%>а̄ V11_MUTUAL_PASS_PRS ;
+Err/Orth:%{VU%}%>я̄ V11_MUTUAL_PASS_PRS ;
:%{VU%} PASS_COND ;

LEXICON V_U2C !This has two consonants before -уӈкве (voiced)
!! put derivations here
+Der+Der/Vlt+V:%>алт V0_A ;
+Der+Der/Vnt+V:%>ант V0_A ;
+Der+Der/Vasa+V:%>ас V0_A ;!2024-04-08
+Der+Der/Vyglal+V:%>ыгла̄л V0_U ;
+Der+Der/Vyglal+Err/Orth+V:%>ыглал V0_U ;
+Der+Der/Vyschl:%>ыщл V0_A ;
 V0_U2C ;
 
LEXICON V0_U2C !This has two consonants before -уӈкве (voiced)
!! **LEXICON @LEXNAME@** 
:%{VU%}%>%{уюØ%} INF ;
:%{VU%} PRSPRC ; ! 2024-04-08
!:%{VU%} V11_MUTUAL ;
:%{VU%}%>%{уюØ%} PRFPRC ;
:%{VU%}%>%{ыиØ%} GER ;!+Ger?+Actio
:%{VU%}%>%{ыиØ%} ACTIO ;
+Der+Der/Ap+N:%{VU%}%>ап N_VCH ;
!:%{VU%} ACT_IND_VU ;  2023-11-18
:%{VU%} ACT_IND_PRS_VU ;!2023-11-18


:%{VU%} ACT_IND_PRT_OCDU/PL_VU ;!2023-11-18 JOXTUNGKVE ы%{VU%}
 
:%{VU%} ACT_IMPRT_VU ;!all
:%{VU%} ACT_COND_VU ;!all
:%{VU%} ACT_IND_PRT_SC_VU ;!2023-11-18 JOXTUNGKVE ы%{VU%}

:%{VU%} ACT_IND_PRT_OCSG_slameen ;
+Act+Ind+Prt+ScPl3+OcSg:%{VU%}%>ысаныл VERB_K ;!2023-11-18 NEEDWORK

:%{VU%} PASS_PRT ;!2023-11-18 JOXTUNGKVE %{VU%}у
:%{VU%}%>%{аяØ%} PASS_PRS_VU ;!CHECKME
+Err/Orth:%{VU%}%>а̄ V11_MUTUAL_PASS_PRS ;
+Err/Orth:%{VU%}%>я̄ V11_MUTUAL_PASS_PRS ;
:%{VU%} PASS_COND ;

LEXICON V_Y/U
!! put derivations here
+Der+Der/Vlt+V:%>алт V0_A ;
+Der+Der/Vnt+V:%>ант V0_A ;
+Der+Der/Vasa+V:%>ас V0_A ;!2024-04-08
+Der+Der/Vyglal+V:%>ыгла̄л V0_U ;
+Der+Der/Vyglal+Err/Orth+V:%>ыглал V0_U ;
+Der+Der/Vyschl:%>ыщл V0_A ;
 V0_Y/U ;
LEXICON V0_Y/U !This has two consonants, one is onset before -уӈкве
!(вл|гл|пл|сл|йл|щл|тл)уӈкве
!(рг|мг|нг|сг)уӈкве
!(рс|мс|мщ|хщ|ньщ)уӈкве
!(км|вм|рм|йм)уӈкве
!(гр|ӈр)уӈкве
!(ӈх)уӈкве
!(рн)уӈкве
!! **LEXICON @LEXNAME@** 
:%{VU%}%>%{уюØ%} INF ;
:%{VU%}ы PRSPRC ; ! 2023-11-17
!:%{VU%} V11_MUTUAL ;
:%{VU%}%>%{уюØ%} PRFPRC ;
:%{VU%}%>%{ыиØ%} GER ;!+Ger?+Actio
:%{VU%}%>%{ыиØ%} ACTIO ;
+Der+Der/Ap+N:%{VU%}%>ап N_VCH ;
!:%{VU%} ACT_IND_VU ;  2023-11-18
:%{VU%} ACT_IND_PRS_VU ;!2023-11-18


:%{VU%}ы ACT_IND_PRT_OCDU/PL_VU ;!2023-11-18
 
:%{VU%} ACT_IMPRT_VU ;!all
:%{VU%} ACT_COND_VU ;!all
:%{VU%}ы ACT_IND_PRT_SC_VU ;!2023-11-18

:%{VU%} ACT_IND_PRT_OCSG_slameen ;
+Act+Ind+Prt+ScPl3+OcSg:%{VU%}%>ысаныл VERB_K ;!2023-11-18 NEEDWORK

:%{VU%}у PASS_PRT ;!2023-11-18 
:%{VU%}%>%{аяØ%} PASS_PRS_VU ;!CHECKME
+Err/Orth:%{VU%}%>а̄ V11_MUTUAL_PASS_PRS ;
+Err/Orth:%{VU%}%>я̄ V11_MUTUAL_PASS_PRS ;
:%{VU%} PASS_COND ;

LEXICON INF
+Inf:ӈкве VERB_K ;
+Inf+Err/Orth:ӈкв VERB_K ;

LEXICON V_A
!! put derivations here
 V0_A ;
LEXICON V0_A
!! **LEXICON @LEXNAME@** 
:%{VA%}%>%{аяØ%} INF ;
:%{VA%}%>%{аяØ%} PRSPRC ; !Does this exist ?20230504 
+PrsPrc:%{VA%}%>%{аяØ%}н # ; ! This will need some variants with onset vowel.
:%{VA%} V11_MUTUAL ;
:%{VA%}%>%{аяØ%} ACT_IND_PRT_SC_VA ;
:%{VA%}%>%{аяØ%} V11_MUTUAL_PASS ;
:%{VA%}%>%{аяØ%} V11_MUTUAL_IND_PRT_OC3 ;

! extras for V_A 2023_04_29
:%{VA%}%>%{аяØ%} ACT_COND_VU ;
:%{VA%}	ACT_IMPRT_for_V_A ;
:%{VA%}%>%{аяØ%} PASS_COND ;!add vowel where there was none 2023_05_02

LEXICON V11_MUTUAL 
!! * **LEXICON @LEXNAME@** 
:%>%{аяØ%} PRFPRC ;
:%>%{ыиØ%} GER ;!ым +Ger?+Actio
:%>%{ыиØ%} ACTIO ;
+Der+Der/Ap+N:%>ап N_VCH ;
 ACT_IND_PRS_VA ;
 ACT_IMPRT ;
! V11_MUTUAL-COND	  ;
!
LEXICON ACT_IND_PRS_VA 
!! 	* **LEXICON @LEXNAME@**  
+Act+Ind+Prs+ScSg1:%>%{ЭЕLong%}гум VERB_K ;
+Err/Orth+Act+Ind+Prs+ScSg2:%>%{ЭЕ%}гын VERB_K ;
+Act+Ind+Prs+ScSg2:%>%{ЭЕLong%}гын VERB_K ;
+Act+Ind+Prs+ScSg3:%>%{ЫИ%} VERB_K ;
+Act+Ind+Prs+ScDu1:%>%{ЫИ%}ме̄н VERB_K ;
+Act+Ind+Prs+ScDu2:%>%{ЭЕLong%}гын VERB_K ;
+Act+Ind+Prs+ScDu2:%>%{ЭЕ%}гы̄н VERB_K ;
+Act+Ind+Prs+ScDu3:%>%{ЭЕLong%}г VERB_K ;
+Act+Ind+Prs+ScPl1:%>%{ЭЕLong%}в VERB_K ;
+Err/Orth+Act+Ind+Prs+ScPl1:%>%{ЭЕ%}в VERB_K ;
+Act+Ind+Prs+ScPl2:%>%{ЭЕLong%}гын VERB_K ;
+Act+Ind+Prs+ScPl2:%>%{ЭЕ%}гы̄н VERB_K ;
+Act+Ind+Prs+ScPl3:%>%{ЭЕLong%}гыт VERB_K ;
!
:%>%{ЫИ%} ACT_IND_PRS_OCSG_lameen ;
:%>%{ЫИ%} ACT_IND_PRS_OCDU ;
:%>%{ЫИ%} ACT_IND_PRS_OCPL ;

LEXICON ACT_IND_PRS_OCSG_lameen
+Act+Ind+Prs+ScSg1+OcSg:лум VERB_K ;
+Act+Ind+Prs+ScSg2+OcSg:лын VERB_K ;
+Act+Ind+Prs+ScSg3+OcSg:тэ VERB_K ; ! XXX Long ? тотытэ vs. тоты̄тэ
+Act+Ind+Prs+ScDu1+OcSg:ламе̄н VERB_K ;!distinction
+Act+Ind+Prs+ScDu2+OcSg:лын VERB_K ;
+Act+Ind+Prs+ScDu2+OcSg:лы̄н VERB_K ;
+Act+Ind+Prs+ScDu3+OcSg:тэ̄н VERB_K ;
+Act+Ind+Prs+ScPl1+OcSg:лув VERB_K ;
+Act+Ind+Prs+ScPl2+OcSg:лын VERB_K ;
+Act+Ind+Prs+ScPl2+OcSg:лы̄н VERB_K ;
+Act+Ind+Prs+ScPl3+OcSg:%>%{аяØ%}ныл VERB_K ;
+Act+Ind+Prs+ScPl3+OcSg+Err/Orth:%>%{аяLong%}ныл VERB_K ;

LEXICON ACT_IND_PRS_OCSG_lumeen
+Act+Ind+Prs+ScSg1+OcSg:лум VERB_K ;
+Act+Ind+Prs+ScSg2+OcSg:лын VERB_K ;
+Act+Ind+Prs+ScSg3+OcSg:тэ VERB_K ; ! XXX Long ? тотытэ vs. тоты̄тэ
+Act+Ind+Prs+ScDu1+OcSg:луме̄н VERB_K ;!distinction
+Act+Ind+Prs+ScDu2+OcSg:лын VERB_K ;
+Act+Ind+Prs+ScDu2+OcSg:лы̄н VERB_K ;
+Act+Ind+Prs+ScDu3+OcSg:тэ̄н VERB_K ;
+Act+Ind+Prs+ScDu3+OcSg+Err/Orth:тэн VERB_K ;
+Act+Ind+Prs+ScPl1+OcSg:лув VERB_K ;
+Act+Ind+Prs+ScPl1+OcSg:лӯв VERB_K ;! JMR 20230503 and тотылӯв and лыглӯв 20230901
+Act+Ind+Prs+ScPl2+OcSg:лын VERB_K ;
+Act+Ind+Prs+ScPl2+OcSg:лы̄н VERB_K ;
+Act+Ind+Prs+ScPl3+OcSg:%>%{аяØ%}ныл VERB_K ;
+Act+Ind+Prs+ScPl3+OcSg+Err/Orth:%>%{аяLong%}ныл VERB_K ;

LEXICON ACT_IND_PRS_OCDU
!
+Act+Ind+Prs+ScSg1+OcDu:%>%{аяØ%}гум VERB_K ;
+Act+Ind+Prs+ScSg2+OcDu:%>%{аяØ%}гын VERB_K ;
+Act+Ind+Prs+ScSg2+OcDu+Err/Orth:%>%{аяØ%}гн VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR V-6-tengkwe
+Act+Ind+Prs+ScSg2+OcDu+Err/Orth:%>%{аяØ%}гнын VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR V-6-tengkwe
+Act+Ind+Prs+ScSg3+OcDu:%>%{аяØ%}ге VERB_K ;
+Act+Ind+Prs+ScDu1+OcDu+Err/Orth:%>%{аяØ%}гмен VERB_K ;
+Act+Ind+Prs+ScDu1+OcDu:%>%{аяØ%}гме̄н VERB_K ;
+Act+Ind+Prs+ScDu1+OcDu+Err/Orth:%>%{аяØ%}гаме̄н VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR V-6-tengkwe
+Act+Ind+Prs+ScDu2+OcDu:%>%{аяØ%}гын VERB_K ;
+Act+Ind+Prs+ScDu2+OcDu:%>%{аяØ%}гы̄н VERB_K ;
+Act+Ind+Prs+ScDu2+OcDu+Err/Orth:%>%{аяØ%}гагы̄н VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR V-6-tengkwe
+Act+Ind+Prs+ScDu3+OcDu:%>%{аяØ%}ге̄н VERB_K ;
+Act+Ind+Prs+ScDu3+OcDu+Err/Orth:%>%{аяØ%}гаге̄н VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR V-6-tengkwe
+Act+Ind+Prs+ScDu3+OcDu+Err/Orth:%>%{аяØ%}ген VERB_K ;
+Act+Ind+Prs+ScPl1+OcDu:%>%{аяØ%}гув VERB_K ;
+Act+Ind+Prs+ScPl1+OcDu:%>%{аяØ%}гӯв VERB_K ;
+Act+Ind+Prs+ScPl2+OcDu:%>%{аяØ%}гын VERB_K ;
+Act+Ind+Prs+ScPl2+OcDu:%>%{аяØ%}гы̄н VERB_K ;
+Act+Ind+Prs+ScPl3+OcDu:%>%{аяØ%}ганыл VERB_K ;

LEXICON ACT_IND_PRS_OCPL
!
+Act+Ind+Prs+ScSg1+OcPl:%>%{аяØ%}нум VERB_K ;
+Act+Ind+Prs+ScSg2+OcPl:%>%{аяØ%}нын VERB_K ;
+Act+Ind+Prs+ScSg2+OcPl:%>%{аяØ%}н VERB_K ;
+Act+Ind+Prs+ScSg3+OcPl:%>%{аяØ%}нэ VERB_K ;
+Act+Ind+Prs+ScDu1+OcPl:%>%{аяØ%}наме̄н VERB_K ;
+Act+Ind+Prs+ScDu1+OcPl+Err/Orth:%>%{аяØ%}нанме̄н VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR: V-6-tengkwe
+Act+Ind+Prs+ScDu2+OcPl:%>%{аяØ%}нын VERB_K ;
+Act+Ind+Prs+ScDu2+OcPl:%>%{аяØ%}ны̄н VERB_K ;
+Act+Ind+Prs+ScDu2+OcPl:%>%{аяØ%}н VERB_K ;
+Act+Ind+Prs+ScDu3+OcPl:%>%{аяØ%}нэ̄н VERB_K ;
+Act+Ind+Prs+ScDu3+OcPl+Err/Orth:%>%{аяØ%}нанэ̄н VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR: V-6-tengkwe
+Act+Ind+Prs+ScPl1+OcPl:%>%{аяØ%}нув VERB_K ;
+Act+Ind+Prs+ScPl1+OcPl:%>%{аяØ%}нӯв VERB_K ;
+Act+Ind+Prs+ScPl1+OcPl+Err/Orth:%>%{аяØ%}нанӯв VERB_K ;!Jaska 20230901 AGAINST V-6-lingkwe; FOR: V-6-tengkwe
+Act+Ind+Prs+ScPl2+OcPl:%>%{аяØ%}нын VERB_K ;
+Act+Ind+Prs+ScPl2+OcPl:%>%{аяØ%}ны̄н VERB_K ;
+Act+Ind+Prs+ScPl2+OcPl:%>%{аяØ%}н VERB_K ;
+Act+Ind+Prs+ScPl3+OcPl:%>%{аяØ%}ныл VERB_K ;


!

LEXICON ACT_IND_VU
 ACT_IND_PRS_VU ;
 ACT_IND_PRT_OCDU/PL_VU ;

LEXICON ACT_IND_PRS_VU
!! 	* **LEXICON @LEXNAME@**  
+Act+Ind+Prs+ScSg1:%>%{ЭЕLong%}гум VERB_K ;
+Err/Orth+Act+Ind+Prs+ScSg2:%>%{ЭЕ%}гын VERB_K ;
+Act+Ind+Prs+ScSg2:%>%{ЭЕLong%}гын VERB_K ;
+Act+Ind+Prs+ScSg3:%>%{ЫИ%} VERB_K ;
+Act+Ind+Prs+ScDu1:%>%{ЫИ%}ме̄н VERB_K ;
+Act+Ind+Prs+ScDu2:%>%{ЭЕLong%}гын VERB_K ;
+Use/NG+Act+Ind+Prs+ScDu2:%>%{ЭЕ%}гы̄н VERB_K ;
+Act+Ind+Prs+ScDu3:%>%{ЭЕLong%}г VERB_K ;
+Act+Ind+Prs+ScPl1:%>%{ЭЕLong%}в VERB_K ;
+Err/Orth+Act+Ind+Prs+ScPl1:%>%{ЭЕ%}в VERB_K ;
+Act+Ind+Prs+ScPl2:%>%{ЭЕLong%}гын VERB_K ;
+Use/NG+Act+Ind+Prs+ScPl2:%>%{ЭЕ%}гы̄н VERB_K ;
 ACT_IND_PRS_SCPL3_eegyt ;!CHECKME йӣкве̄гыт

!
:%>%{ЫИ%} ACT_IND_PRS_OCSG_lumeen ;

:%>%{ЫИ%} ACT_IND_PRS_OCDU ;

:%>%{ЫИ%} ACT_IND_PRS_OCPL ;

!


!
LEXICON ACT_IND_PRT_SC_VU 
ACT_IND_PRT_SCSG1_sum ;
ACT_IND_PRT_SCSG2_syn ;
ACT_IND_PRT_SCSG3_ys ;
ACT_IND_PRT_SCDU1_sumeen ;
 ACT_IND_PRT_SCDU2_syn ;
  ACT_IND_PRT_SCDU2_syyn ;
  ACT_IND_PRT_SCDU3_syg ;
  ACT_IND_PRT_SCDU3_syyg ;
 ACT_IND_PRT_SCPL1_suv ;
 ACT_IND_PRT_SCPL1_suuv ;
 ACT_IND_PRT_SCPL2_syn ;
 ACT_IND_PRT_SCPL2_syyn ;
 ACT_IND_PRT_SCPL3_syt ;

LEXICON ACT_IND_PRT_SC_VA
: ACT_IND_PRT_SCSG1_sum ;
: ACT_IND_PRT_SCSG2_syn ;
: ACT_IND_PRT_SCSG3_ys ;
: ACT_IND_PRT_SCDU1_ysmeen ;!Jaska 20230901 mingkwe
: ACT_IND_PRT_SCDU2_syn ;
: ACT_IND_PRT_SCDU2_syyn ;
: ACT_IND_PRT_SCDU3_syg ;
: ACT_IND_PRT_SCDU3_syyg ;
: ACT_IND_PRT_SCPL1_suv ;
: ACT_IND_PRT_SCPL1_suuv ;
: ACT_IND_PRT_SCPL2_syn ;
: ACT_IND_PRT_SCPL2_syyn ;
: ACT_IND_PRT_SCPL3_syt ;
!

!
LEXICON V11_MUTUAL_IND_PRT_OC3
 ACT_IND_PRT_OCSG_slameen ;
 ACT_IND_PRT_OCDU ;
 ACT_IND_PRT_OCPL ;

LEXICON ACT_IND_PRT_OCDU/PL_VU
 ACT_IND_PRT_OCDU ;
 ACT_IND_PRT_OCPL ;


LEXICON ACT_IND_PRT_OCSG_slameen
+Act+Ind+Prt+ScSg1+OcSg:%>ыслум VERB_K ;
+Act+Ind+Prt+ScSg2+OcSg:%>ыслын VERB_K ;
+Act+Ind+Prt+ScSg3+OcSg:%>ыстэ VERB_K ;
+Act+Ind+Prt+ScDu1+OcSg:%>ысламе̄н VERB_K ;!distinction
+Act+Ind+Prt+ScDu2+OcSg:%>ыслын VERB_K ;
+Act+Ind+Prt+ScDu2+OcSg:%>ыслы̄н VERB_K ;
+Act+Ind+Prt+ScDu3+OcSg:%>ыстэ̄н VERB_K ;
+Act+Ind+Prt+ScPl1+OcSg:%>ыслув VERB_K ;!
+Act+Ind+Prt+ScPl1+OcSg:%>ыслӯв VERB_K ;!ва̄рыслӯв
+Act+Ind+Prt+ScPl2+OcSg:%>ыслын VERB_K ;
+Act+Ind+Prt+ScPl2+OcSg:%>ыслы̄н VERB_K ;
+Act+Ind+Prt+ScPl3+OcSg:%>саныл VERB_K ;

LEXICON ACT_IND_PRT_OCSG_slumeen
+Act+Ind+Prt+ScSg1+OcSg:%>ыслум VERB_K ;
+Act+Ind+Prt+ScSg2+OcSg:%>ыслын VERB_K ;
+Act+Ind+Prt+ScSg3+OcSg:%>ыстэ VERB_K ;
+Act+Ind+Prt+ScDu1+OcSg:%>ыслуме̄н VERB_K ;
+Act+Ind+Prt+ScDu2+OcSg:%>ыслын VERB_K ;
+Act+Ind+Prt+ScDu2+OcSg:%>ыслы̄н VERB_K ;
+Act+Ind+Prt+ScDu3+OcSg:%>ыстэ̄н VERB_K ;
+Act+Ind+Prt+ScPl1+OcSg:%>ыслув VERB_K ;!
+Act+Ind+Prt+ScPl1+OcSg:%>ыслӯв VERB_K ;!ва̄рыслӯв
+Act+Ind+Prt+ScPl2+OcSg:%>ыслын VERB_K ;
+Act+Ind+Prt+ScPl2+OcSg:%>ыслы̄н VERB_K ;
+Act+Ind+Prt+ScPl3+OcSg:%>саныл VERB_K ;

!
LEXICON ACT_IND_PRT_OCDU
+Act+Ind+Prt+ScSg1+OcDu:%>сагум VERB_K ;
+Act+Ind+Prt+ScSg2+OcDu:%>сагын VERB_K ;
+Act+Ind+Prt+ScSg3+OcDu:%>саге VERB_K ;
+Act+Ind+Prt+ScDu1+OcDu:%>сагме̄н VERB_K ;
+Act+Ind+Prt+ScDu2+OcDu:%>сагын VERB_K ;
+Act+Ind+Prt+ScDu2+OcDu:%>сагы̄н VERB_K ;
+Act+Ind+Prt+ScDu3+OcDu:%>саге̄н VERB_K ;
+Act+Ind+Prt+ScPl1+OcDu:%>сагув VERB_K ;
+Act+Ind+Prt+ScPl1+OcDu:%>сагӯв VERB_K ;
+Act+Ind+Prt+ScPl2+OcDu:%>сагын VERB_K ;
+Act+Ind+Prt+ScPl2+OcDu:%>сагы̄н VERB_K ;
+Err/Orth+Act+Ind+Prt+ScPl3+OcDu:%>саныл VERB_K ;
+Act+Ind+Prt+ScPl3+OcDu:%>саганыл VERB_K ;
!
LEXICON ACT_IND_PRT_OCPL
+Act+Ind+Prt+ScSg1+OcPl:%>санум VERB_K ;
+Act+Ind+Prt+ScSg2+OcPl:%>са̄нын VERB_K ;
+Act+Ind+Prt+ScSg2+OcPl:%>сан VERB_K ;
+Act+Ind+Prt+ScSg3+OcPl:%>санэ VERB_K ;
+Act+Ind+Prt+ScDu1+OcPl:%>санаме̄н VERB_K ;
+Act+Ind+Prt+ScDu1+OcPl:%>санме̄н VERB_K ;
+Act+Ind+Prt+ScDu2+OcPl:%>санын VERB_K ;
+Act+Ind+Prt+ScDu2+OcPl:%>саны̄н VERB_K ;
+Act+Ind+Prt+ScDu2+OcPl:%>сан VERB_K ;
+Act+Ind+Prt+ScDu3+OcPl:%>санэ̄н VERB_K ;
+Act+Ind+Prt+ScPl1+OcPl:%>санув VERB_K ;!
+Act+Ind+Prt+ScPl1+OcPl+Err/Orth-long-v:%>санӯв VERB_K ;!по̄слысанӯв
+Act+Ind+Prt+ScPl2+OcPl:%>санын VERB_K ;
+Act+Ind+Prt+ScPl2+OcPl:%>саны̄н VERB_K ;
+Act+Ind+Prt+ScPl2+OcPl:%>сан VERB_K ;
+Act+Ind+Prt+ScPl3+OcPl:%>саныл VERB_K ;

!

LEXICON ACT_COND_VU 
!! 	* **LEXICON @LEXNAME@** 
+Act+Cond+ScSg1:%>нувум VERB_K ;
+Act+Cond+ScSg1:%>нӯвум VERB_K ;
+Act+Cond+ScSg2:%>нувын VERB_K ;
+Act+Cond+ScSg2:%>нӯвын VERB_K ;
+Act+Cond+ScSg3:%>нув VERB_K ;
+Act+Cond+ScDu1:%>нуваме̄н VERB_K ;
+Act+Cond+ScDu2:%>нувын VERB_K ;
+Act+Cond+ScDu2:%>нувы̄н VERB_K ;
+Act+Cond+ScDu3:%>нувыг VERB_K ;
+Act+Cond+ScDu3:%>нувы̄г VERB_K ;
+Act+Cond+ScPl1:%>нувув VERB_K ;
+Act+Cond+ScPl1:%>нувӯв VERB_K ;
+Act+Cond+ScPl2:%>нувын VERB_K ;
+Act+Cond+ScPl2:%>нувы̄н VERB_K ;
+Act+Cond+ScPl3:%>нувыт VERB_K ;
+Act+Cond+ScPl3:%>нувы̄т VERB_K ;
!
+Act+Cond+ScSg1+OcSg:%>нувлум VERB_K ;
+Act+Cond+ScSg1+OcSg+Err/Orth-long-v:%>нӯвлум VERB_K ;!CHECKME Err/Orth
+Act+Cond+ScSg2+OcSg:%>нувлын VERB_K ;
+Act+Cond+ScSg2+OcSg+Err/Orth-long-v:%>нӯвлын VERB_K ;!CHECKME Err/Orth
+Act+Cond+ScSg3+OcSg:%>нувтэ VERB_K ;
+Act+Cond+ScSg3+OcSg+Err/Orth-long-v:%>нӯвтэ VERB_K ;!CHECKME Err/Orth
+Act+Cond+ScDu1+OcSg:%>нувламе̄н VERB_K ;
+Act+Cond+ScDu2+OcSg:%>нувлын VERB_K ;
+Act+Cond+ScDu2+OcSg:%>нувлы̄н VERB_K ;
+Act+Cond+ScDu3+OcSg:%>нувтэ̄н VERB_K ;
+Act+Cond+ScPl1+OcSg:%>нувлув VERB_K ;
+Act+Cond+ScPl1+OcSg:%>нувлӯв VERB_K ;
+Act+Cond+ScPl2+OcSg:%>нувлын VERB_K ;
+Act+Cond+ScPl2+OcSg:%>нувлы̄н VERB_K ;
+Act+Cond+ScPl3+OcSg:%>нуваныл VERB_K ;
+Act+Cond+ScPl3+OcSg:%>нува̄ныл VERB_K ;
!
+Act+Cond+ScSg1+OcDu:%>нувагум VERB_K ;
+Act+Cond+ScSg1+OcDu:%>нува̄гум VERB_K ;
+Act+Cond+ScSg2+OcDu:%>нувагын VERB_K ;
+Act+Cond+ScSg2+OcDu:%>нува̄гын VERB_K ;
+Act+Cond+ScSg3+OcDu:%>нуваге VERB_K ;
+Act+Cond+ScSg3+OcDu:%>нува̄ге VERB_K ;
+Act+Cond+ScDu1+OcDu:%>нувагме̄н VERB_K ;
+Act+Cond+ScDu2+OcDu:%>нувагын VERB_K ;
+Act+Cond+ScDu2+OcDu:%>нувагы̄н VERB_K ;
+Act+Cond+ScDu3+OcDu:%>нуваге̄н VERB_K ;
+Act+Cond+ScPl1+OcDu:%>нувагув VERB_K ;
+Act+Cond+ScPl1+OcDu:%>нувагӯв VERB_K ;
+Act+Cond+ScPl2+OcDu:%>нувагын VERB_K ;
+Act+Cond+ScPl2+OcDu:%>нувагы̄н VERB_K ;
+Act+Cond+ScPl3+OcDu:%>нуваганыл VERB_K ;
+Act+Cond+ScPl3+OcDu:%>нувага̄ныл VERB_K ;
!
+Act+Cond+ScSg1+OcPl:%>нуванум VERB_K ;
+Act+Cond+ScSg1+OcPl:%>нува̄нум VERB_K ;
+Act+Cond+ScSg2+OcPl:%>нуванын VERB_K ;
+Act+Cond+ScSg2+OcPl:%>нува̄нын VERB_K ;
+Act+Cond+ScSg3+OcPl:%>нуванэ VERB_K ;
+Act+Cond+ScSg3+OcPl:%>нува̄нэ VERB_K ;
+Act+Cond+ScDu1+OcPl:%>нуванме̄н VERB_K ;
+Act+Cond+ScDu2+OcPl:%>нуванын VERB_K ;
+Act+Cond+ScDu2+OcPl:%>нуваны̄н VERB_K ;
+Act+Cond+ScDu3+OcPl:%>нуванэ̄н VERB_K ;!CHECKME ва̄нуванэ̄н
+Act+Cond+ScPl1+OcPl:%>нуванув VERB_K ;
+Act+Cond+ScPl2+OcPl:%>нуванын VERB_K ;
+Act+Cond+ScPl2+OcPl:%>нуваны̄н VERB_K ;
+Act+Cond+ScPl3+OcPl:%>нуваныл VERB_K ;
+Act+Cond+ScPl3+OcPl:%>нува̄ныл VERB_K ;


!
LEXICON ACT_IMPRT  
!! 	* **LEXICON @LEXNAME@**  
+Act+Imprt+ScSg2:%>%{ЭЕ%}н VERB_K ;
+Act+Imprt+ScDu2:%>%{ЭЕ%}н VERB_K ;
+Act+Imprt+ScDu2:%>%{ЭЕLong%}н VERB_K ;
+Act+Imprt+ScPl2:%>%{ЭЕLong%}н VERB_K ;
+Act+Imprt+ScPl2:%>%{ЭЕ%}н VERB_K ;
!
+Act+Imprt+ScSg2+OcSg:%>%{ЭЕ%}лн VERB_K ;
+Act+Imprt+ScSg2+OcSg:%>%{ЭЕLong%}лн VERB_K ;
+Act+Imprt+ScDu2+OcSg:%>%{ЭЕLong%}лн VERB_K ;
+Act+Imprt+ScDu2+OcSg:%>%{ЭЕLong%}лы̄н VERB_K ;
+Act+Imprt+ScPl2+OcSg:%>%{ЭЕLong%}лн VERB_K ;
+Act+Imprt+ScPl2+OcSg:%>%{ЭЕLong%}лы̄н VERB_K ;
!
+Act+Imprt+ScSg2+OcDu:%>%{ЭЕ%}гын VERB_K ;
+Act+Imprt+ScSg2+OcDu:%>%{ЭЕLong%}гн VERB_K ;
+Act+Imprt+ScDu2+OcDu:%>%{ЭЕ%}гын VERB_K ;
+Act+Imprt+ScDu2+OcDu:%>%{ЭЕ%}гы̄н VERB_K ;
+Act+Imprt+ScPl2+OcDu:%>%{ЭЕ%}гын VERB_K ;
+Act+Imprt+ScPl2+OcDu:%>%{ЭЕ%}гы̄н VERB_K ;
!
+Act+Imprt+ScSg2+OcPl:%>%{ЭЕ%}н VERB_K ;
+Act+Imprt+ScSg2+OcPl:%>%{ЭЕLong%}н VERB_K ;
+Act+Imprt+ScSg2+OcPl+Err/Orth:%>%{ЭЕLong%}нын VERB_K ;!Jaska 20230901, not in V-6-tengkwe
+Act+Imprt+ScDu2+OcPl:%>%{ЭЕ%}н VERB_K ;
+Act+Imprt+ScDu2+OcPl:%>%{ЭЕLong%}н VERB_K ;
+Act+Imprt+ScDu2+OcPl+Err/Orth:%>%{ЭЕLong%}нын VERB_K ;!Jaska 20230901, AGAINST V-6-tengkwe ; FOR V-6-mingkwe, wingkwe, V-6-lingkwe
+Act+Imprt+ScDu2+OcPl+Err/Orth:%>%{ЭЕLong%}ны̄н VERB_K ;!Jaska 20230901, but not in V-6-tengkwe
+Act+Imprt+ScPl2+OcPl:%>%{ЭЕLong%}н VERB_K ;
+Act+Imprt+ScPl2+OcPl:%>%{ЭЕ%}н VERB_K ;
+Act+Imprt+ScPl2+OcPl+Err/Orth:%>%{ЭЕLong%}нын VERB_K ;!Jaska 20230901,  AGAINST V-6-tengkwe ; FOR V-6-mingkwe, wingkwe, V-6-lingkwe
+Act+Imprt+ScPl2+OcPl+Err/Orth:%>%{ЭЕLong%}ны̄н VERB_K ;!Jaska 20230901, but not in V-6-tengkwe

LEXICON ACT_IMPRT_VU
!! 	* **LEXICON @LEXNAME@**  
+Act+Imprt+ScSg2:%>%{ЭЕ%}н VERB_K ;
+Act+Imprt+ScDu2:%>%{ЭЕ%}н VERB_K ;
+Act+Imprt+ScDu2:%>%{ЭЕLong%}н VERB_K ;
+Act+Imprt+ScPl2:%>%{ЭЕLong%}н VERB_K ;
+Act+Imprt+ScPl2:%>%{ЭЕ%}н VERB_K ;
!
+Act+Imprt+ScSg2+OcSg:%>%{ЭЕ%}лн VERB_K ;
+Act+Imprt+ScSg2+OcSg:%>%{ЭЕLong%}лн VERB_K ;
+Act+Imprt+ScDu2+OcSg:%>%{ЭЕLong%}лн VERB_K ;
+Act+Imprt+ScDu2+OcSg:%>%{ЭЕLong%}лы̄н VERB_K ;
+Act+Imprt+ScPl2+OcSg:%>%{ЭЕLong%}лн VERB_K ;
+Act+Imprt+ScPl2+OcSg:%>%{ЭЕLong%}лы̄н VERB_K ;
!
+Act+Imprt+ScSg2+OcDu:%>%{ЭЕ%}гын VERB_K ;
+Act+Imprt+ScSg2+OcDu:%>%{ЭЕLong%}гн VERB_K ;
+Act+Imprt+ScDu2+OcDu:%>%{ЭЕ%}гын VERB_K ;
+Act+Imprt+ScDu2+OcDu:%>%{ЭЕ%}гы̄н VERB_K ;
+Act+Imprt+ScPl2+OcDu:%>%{ЭЕ%}гын VERB_K ;
+Act+Imprt+ScPl2+OcDu:%>%{ЭЕ%}гы̄н VERB_K ;
!
+Act+Imprt+ScSg2+OcPl:%>%{ЭЕ%}н VERB_K ;
+Act+Imprt+ScSg2+OcPl:%>%{ЭЕLong%}н VERB_K ;
+Act+Imprt+ScSg2+OcPl:%>%{ЭЕLong%}нын VERB_K ;! Jaska 20230901 V-6-wingkwe
+Act+Imprt+ScDu2+OcPl:%>%{ЭЕ%}н VERB_K ;
+Act+Imprt+ScDu2+OcPl:%>%{ЭЕLong%}н VERB_K ;
+Act+Imprt+ScDu2+OcPl:%>%{ЭЕLong%}ны̄н VERB_K ;! Jaska	20230901 V-6-wingkwe
+Act+Imprt+ScDu2+OcPl+Err/Orth:%>%{ЭЕLong%}нын VERB_K ;! Jaska 20230901 AGAINST V-6-mingkwe, wingkwe ; FOR V-6-lingkwe
+Act+Imprt+ScPl2+OcPl:%>%{ЭЕLong%}н VERB_K ;
+Act+Imprt+ScPl2+OcPl:%>%{ЭЕLong%}ны̄н VERB_K ;! Jaska	20230901  V-6-wingkwe
+Act+Imprt+ScPl2+OcPl+Err/Orth:%>%{ЭЕLong%}нын VERB_K ;! Jaska 20230901 AGAINST V-6-mingkwe, wingkwe ; FOR V-6-lingkwe
+Act+Imprt+ScPl2+OcPl:%>%{ЭЕ%}н VERB_K ;

LEXICON ACT_IMPRT_for_V_A
+Act+Imprt+ScDu2:%>%{ЭЕLong%}н VERB_K ;
+Act+Imprt+ScSg2+OcPl:%>%{ЭЕLong%}н VERB_K ;
+Act+Imprt+ScDu2+OcPl:%>%{ЭЕLong%}н VERB_K ;
!
LEXICON V11_MUTUAL_PASS
 V11_MUTUAL_PASS_PRS ;
 PASS_PRT ;
LEXICON V11_MUTUAL_PASS_PRS
!! 	* **LEXICON @LEXNAME@**  
+Pass+Ind+Prs+ScSg1:%>вем VERB_K ;
+Pass+Ind+Prs+ScSg2:%>вен VERB_K ;
+Pass+Ind+Prs+ScSg3:%>ве VERB_K ;
+Pass+Ind+Prs+ScDu1:%>веме̄н VERB_K ;
+Pass+Ind+Prs+ScDu2:%>ве̄н VERB_K ;!2023 ловиньтаӈкве
+Pass+Ind+Prs+ScDu2:%>вен VERB_K ;
+Pass+Ind+Prs+ScDu3:%>ве̄г VERB_K ;
+Pass+Ind+Prs+ScPl1:%>ве̄в VERB_K ;
+Pass+Ind+Prs+ScPl2:%>ве̄н VERB_K ;!2023 ловиньтаӈкве
+Pass+Ind+Prs+ScPl2:%>вен VERB_K ;
+Pass+Ind+Prs+ScPl3:%>вет VERB_K ;
+Err/Orth+Pass+Ind+Prs+ScDu1:%>вемена VERB_K ;!2023 ловиньтаӈкве
+Err/Orth+Pass+Ind+Prs+ScDu2:%>вена VERB_K ;!2023 ловиньтаӈкве
+Err/Orth+Pass+Ind+Prs+ScDu3:%>вега VERB_K ;!2023 ловиньтаӈкве
+Err/Orth+Pass+Ind+Prs+ScPl1:%>вева VERB_K ;!2023 ловиньтаӈкве
+Err/Orth+Pass+Ind+Prs+ScPl2:%>вена VERB_K ;!2023 ловиньтаӈкве


LEXICON PASS_PRS_VU
!! 	* **LEXICON @LEXNAME@**  
+Pass+Ind+Prs+ScSg1:%>вем VERB_K ;
+Pass+Ind+Prs+ScSg2:%>вен VERB_K ;
+Pass+Ind+Prs+ScSg3:%>ве VERB_K ;
+Pass+Ind+Prs+ScDu1:%>веме̄н VERB_K ;
+Pass+Ind+Prs+ScDu2:%>вен VERB_K ;
+Pass+Ind+Prs+ScDu2:%>ве̄н VERB_K ;
+Pass+Ind+Prs+ScDu3:%>ве̄г VERB_K ;
+Pass+Ind+Prs+ScPl1:%>ве̄в VERB_K ;
+Pass+Ind+Prs+ScPl2:%>вен VERB_K ;
+Pass+Ind+Prs+ScPl2:%>ве̄н VERB_K ;
+Pass+Ind+Prs+ScPl3:%>вет VERB_K ;


!
LEXICON PASS_PRT
+Pass+Ind+Prt+ScSg1:%>ве̄сум VERB_K ;
+Pass+Ind+Prt+ScSg2:%>ве̄сын VERB_K ;
+Pass+Ind+Prt+ScSg3:%>вес VERB_K ;
+Pass+Ind+Prt+ScSg3:%>ве̄с VERB_K ;
+Pass+Ind+Prt+ScDu1:%>весаме̄н VERB_K ;!CHECKME
+Pass+Ind+Prt+ScDu2:%>весы̄н VERB_K ;
+Pass+Ind+Prt+ScDu2:%>весын VERB_K ;
+Pass+Ind+Prt+ScDu3:%>весы̄г VERB_K ;
+Pass+Ind+Prt+ScPl1:%>весув VERB_K ;
+Pass+Ind+Prt+ScPl1:%>весӯв VERB_K ;!янытлавесӯв Jaska 20230901, AGAINST V-6-lingkwe, tengkwe, wingkwe, mingkwe ;FOR V-6-jingkwe
+Pass+Ind+Prt+ScPl2:%>весы̄н VERB_K ;
+Pass+Ind+Prt+ScPl2:%>весын VERB_K ;
+Pass+Ind+Prt+ScPl3:%>ве̄сыт VERB_K ;!Luima seripos
+Pass+Ind+Prt+ScPl3+Err/Orth:%>весыт VERB_K ;!Only in Oppikirja 
+Pass+Ind+Prs+ScPl3+Err/Orth:%>вет VERB_K ;!This also exists 2024-02-08


LEXICON PASS_COND
+Pass+Cond+ScSg1:%>нувем VERB_K ;
+Pass+Cond+ScSg1:%>нӯвем VERB_K ;
+Pass+Cond+ScSg2:%>нӯвен VERB_K ;
+Pass+Cond+ScSg2:%>нувен VERB_K ;
+Pass+Cond+ScSg3:%>нуве VERB_K ;
+Pass+Cond+ScSg3:%>нӯве VERB_K ;
+Pass+Cond+ScDu1:%>нувеме̄н VERB_K ;
+Pass+Cond+ScDu2:%>нуве̄н VERB_K ;
+Pass+Cond+ScDu2:%>нувен VERB_K ;
+Pass+Cond+ScDu3:%>нувег VERB_K ;
+Pass+Cond+ScDu3:%>нуве̄г VERB_K ;
+Pass+Cond+ScPl1:%>нуве̄в VERB_K ;
+Pass+Cond+ScPl2:%>нуве̄н VERB_K ;
+Pass+Cond+ScPl2:%>нувен VERB_K ;
+Pass+Cond+ScPl3:%>нувет VERB_K ;
+Pass+Cond+ScPl3:%>нӯвет VERB_K ;

LEXICON PASS_IMPRT
+Pass+Imprt+ScSg2:%>вен VERB_K ;
+Pass+Imprt+ScSg2:%>ен VERB_K ;
+Pass+Imprt+ScDu2:%>вен VERB_K ;
+Pass+Imprt+ScDu2:%>ве̄н VERB_K ;
+Pass+Imprt+ScDu2:%>ен VERB_K ;
+Pass+Imprt+ScDu2:%>е̄н VERB_K ;
+Pass+Imprt+ScPl2:%>вен VERB_K ;
+Pass+Imprt+ScPl2:%>ве̄н VERB_K ;
+Pass+Imprt+ScPl2:%>ен VERB_K ;
+Pass+Imprt+ScPl2:%>е̄н VERB_K ;

LEXICON PASS_IMPRT_teegveen
+Pass+Imprt+ScSg2:%>вен VERB_K ;
+Pass+Imprt+ScDu2:%>вен VERB_K ;
+Pass+Imprt+ScDu2:%>ве̄н VERB_K ;
+Pass+Imprt+ScPl2:%>вен VERB_K ;
+Pass+Imprt+ScPl2:%>ве̄н VERB_K ;

LEXICON PRSPRC
+PrsPrc:%>нэ VERB_K ; !Does this exist ?20230504
+PrsPrc+PxSg1:%>нэ̄нум VERB_K ;
+PrsPrc+PxSg1:%>нэ̄м VERB_K ;!this exists 2024-02-08
+PrsPrc+PxSg2:%>нэ̄н VERB_K ;
+PrsPrc+PxSg3:%>нэ̄нэ VERB_K ;
+PrsPrc+PxSg3:%>нэ̄тэ VERB_K ;!this exists 2024-02-08
+PrsPrc+PxDu1:%>нэнаме̄н VERB_K ;
+PrsPrc+PxDu1:%>нэнанме̄н VERB_K ;
+PrsPrc+PxDu2:%>нэнаны̄н VERB_K ;
+PrsPrc+PxDu2:%>нэныны̄н VERB_K ;
+PrsPrc+PxDu2:%>нэнаны̄н VERB_K ;
+PrsPrc+PxDu2:%>нэны̄н VERB_K ;
+PrsPrc+PxDu3:%>нэнэ̄н VERB_K ;
+PrsPrc+PxDu3:%>нэнанэ̄н VERB_K ;!
+PrsPrc+PxPl1:%>нэнув VERB_K ;
+PrsPrc+PxPl1:%>нэнанув VERB_K ;
+PrsPrc+PxPl1:%>нэнанӯв VERB_K ;
+PrsPrc+PxPl1:%>нэнӯв VERB_K ;
+PrsPrc+PxPl1:%>нэ̄в VERB_K ;!This exists 2024-02-08
+PrsPrc+PxPl2:%>нэнаны̄н VERB_K ;
+PrsPrc+PxPl2:%>нэны̄н VERB_K ;!
+PrsPrc+PxPl3:%>нэ̄наныл VERB_K ;
+PrsPrc+PxPl3:%>нэ̄нл VERB_K ;!Does this exist 2024-02-08
+PrsPrc+PxPl3:%>нэ̄ныл VERB_K ;!

LEXICON PRSPRC_jingkve
+PrsPrc:%>нэ VERB_K ; !Does this exist ?20230504
+PrsPrc+PxSg1:%>нэм VERB_K ;
+PrsPrc+PxSg1:%>нэ̄м VERB_K ;!this exists 2024-02-08
+PrsPrc+PxSg2:%>нэн VERB_K ;
+PrsPrc+PxSg3:%>нэ̄тэ VERB_K ;
+PrsPrc+PxDu1:%>нэме̄н VERB_K ;
+PrsPrc+PxDu2:%>нэнэ̄н VERB_K ;
+PrsPrc+PxDu3:%>нэ̄г VERB_K ;
+PrsPrc+PxDu3:%>нэг VERB_K ;!
+PrsPrc+PxPl1:%>нэ̄в VERB_K ;
+PrsPrc+PxPl2:%>нэн VERB_K ;
+PrsPrc+PxPl2:%>нэ̄н VERB_K ;!
+PrsPrc+PxPl3:%>нэ̄ныл VERB_K ;
+PrsPrc+PxPl3:%>нэныл VERB_K ;!

LEXICON PRFPRC
 PRFPRC_PLAIN ;
 PRFPRC_PERSON ;
LEXICON PRFPRC_PLAIN
+PrfPrc:%>м VERB_K ;
+PrfPrc+Loc:%>мт VERB_K ;
+PrfPrc+Lat:%>мн VERB_K ;

LEXICON PRFPRC_PERSON
!! the forms below here need studying. They are probably not what I labled them as20230521
+PrfPrc+PxSg1:%>мум VERB_K_CASE ;!о̄лмум, а̄стумум, Ханищтахтамум
+PrfPrc+PxSg2:%>мын VERB_K_CASE ;!ханищтахтамын
+PrfPrc+PxSg3:%>ме VERB_K ;!ва̄рме, а̄стуме, то̄влуме, Ханищтахтаме, Ханищтахтаме̄т, ханищтахтаме̄н
+PrfPrc+Loc+PxSg3:%>ме̄т VERB_K ;
+PrfPrc+Lat+PxSg3:%>ме̄н VERB_K ;
+PrfPrc+PxDu1:%>мме̄н VERB_K_CASE ;!йимме̄н ?not applying vowel loss here 2024-02-06
!+PrfPrc+PxDu1%>муме̄н VERB_K_CASE ;!?о̄лмуме̄н
+PrfPrc+PxDu2:%>мын VERB_K_CASE ;!?
+PrfPrc+PxDu2:%>мы̄н VERB_K_CASE ;!?
+PrfPrc+PxDu3:%>мег VERB_K_CASE ;!?
+PrfPrc+PxDu3:%>ме̄г VERB_K_CASE ;!?
+PrfPrc+PxPl1:%>мув VERB_K_CASE ;!о̄лмувт, о̄лмув, ханищтахтамув
+PrfPrc+PxPl1:%>мӯв VERB_K_CASE ;!
+PrfPrc+PxPl2:%>мын VERB_K_CASE ;!?
+PrfPrc+PxPl2:%>мы̄н VERB_K_CASE ;!?
+PrfPrc+PxPl3:%>мыт VERB_K_CASE ;!

LEXICON PRFPRC_teemanum
+PrfPrc:%>м VERB_K ;
!! the forms below here need studying. They are probably not what I labled them as20230521
+PrfPrc+PxSg1:%>манум VERB_K ;!
+PrfPrc+PxSg1:%>мананум VERB_K ;!
+PrfPrc+PxSg2:%>ман VERB_K ;!
+PrfPrc+PxSg2:%>ма̄нын VERB_K ;!
+PrfPrc+PxSg3:%>ма̄нэ VERB_K ;!
+PrfPrc+PxSg3:%>мана̄нэ VERB_K ;!
+PrfPrc+PxDu1:%>манаме̄н VERB_K ;
+PrfPrc+PxDu1:%>мананме̄н VERB_K ;
+PrfPrc+PxDu2:%>манн VERB_K ;!?
+PrfPrc+PxDu2:%>маны̄н VERB_K ;!?
+PrfPrc+PxDu2:%>мананы̄н VERB_K ;!?
+PrfPrc+PxDu3:%>мананэ̄н VERB_K ;!?
+PrfPrc+PxDu3:%>манэ̄н VERB_K ;!?
+PrfPrc+PxPl1:%>мананув VERB_K ;!
+PrfPrc+PxPl1:%>манӯв VERB_K ;!
+PrfPrc+PxPl1:%>манув VERB_K ;!
+PrfPrc+PxPl1:%>мананӯв VERB_K ;!
+PrfPrc+PxPl2:%>маны̄н VERB_K ;!?
+PrfPrc+PxPl2:%>мананы̄н VERB_K ;!?
+PrfPrc+PxPl3:%>мана̄ныл VERB_K ;!
+PrfPrc+PxPl3:%>ма̄ныл VERB_K ;!

LEXICON ACTIO
+Actio:н A_VCH ; !CHECKME 2023-11-20 for Prefixes

LEXICON GER
+Ger:%>м VERB_K ;
+Ger+PxSg1:%>мам VERB_K ;
+Ger+PxSg2:%>ман VERB_K ;
+Ger+PxSg3:%>ма VERB_K ;! is this actually PxSg3? how does it match with the simple Ger?
+Ger+PxSg3:%>матэ VERB_K ;!Jaska 20230901 V-6-jingkwe, but not in V-6-lingkwe, V-6-wingkwe, yet.
+Ger+PxDu1:%>маме̄н VERB_K ;
+Ger+PxDu2:%>ман VERB_K ;
+Ger+PxDu3:%>маг VERB_K ;
+Ger+PxPl1:%>мав VERB_K ;
+Ger+PxPl2:%>ман VERB_K ;
+Ger+PxPl3:%>мат VERB_K ;

!#Privative
LEXICON VABESS
+VAbess:%>тал VERB_K ;

!! Active Indicative Presence

LEXICON ACT_IND_PRS_SCPL3_yt
+Act+Ind+Prs+ScPl3:%>ыт VERB_K ;
LEXICON ACT_IND_PRS_SCPL3_eegyt
+Act+Ind+Prs+ScPl3:%>%{ЭЕLong%}гыт VERB_K ;


!! Active Indicative Preterite
LEXICON ACT_IND_PRT_SCSG1_sum
+Act+Ind+Prt+ScSg1:%>сум VERB_K ;

LEXICON	ACT_IND_PRT_SCSG2_syn
+Act+Ind+Prt+ScSg2:%>сын VERB_K ;
LEXICON	ACT_IND_PRT_SCSG2_sn
+Act+Ind+Prt+ScSg2:%>сн VERB_K ;
LEXICON	ACT_IND_PRT_SCSG2_snyn
+Act+Ind+Prt+ScSg2:%>снын VERB_K ;

LEXICON ACT_IND_PRT_SCSG3_ys
+Act+Ind+Prt+ScSg3:%>ыс VERB_K ;

LEXICON	ACT_IND_PRT_SCDU1_sumeen
+Act+Ind+Prt+ScDu1:%>суме̄н VERB_K ;!Distinction
LEXICON	ACT_IND_PRT_SCDU1_ysmeen
+Act+Ind+Prt+ScDu1:%>ысме̄н VERB_K ;!Distinction

LEXICON	ACT_IND_PRT_SCDU2_syn
+Act+Ind+Prt+ScDu2:%>сын VERB_K ;
LEXICON	ACT_IND_PRT_SCDU2_syyn
+Act+Ind+Prt+ScDu2:%>сы̄н VERB_K ;

LEXICON	ACT_IND_PRT_SCDU3_syg
+Act+Ind+Prt+ScDu3:%>сыг VERB_K ;
LEXICON	ACT_IND_PRT_SCDU3_syyg
+Act+Ind+Prt+ScDu3:%>сы̄г VERB_K ;!патсы̄г

LEXICON	ACT_IND_PRT_SCPL1_suv
+Act+Ind+Prt+ScPl1:%>сув VERB_K ;
LEXICON	ACT_IND_PRT_SCPL1_suuv
+Act+Ind+Prt+ScPl1:%>сӯв VERB_K ;!хассӯв

LEXICON	ACT_IND_PRT_SCPL2_syn
+Act+Ind+Prt+ScPl2:%>сын VERB_K ;
LEXICON	ACT_IND_PRT_SCPL2_syyn
+Act+Ind+Prt+ScPl2:%>сы̄н VERB_K ;

LEXICON	ACT_IND_PRT_SCPL3_syt
+Act+Ind+Prt+ScPl3:%>сыт VERB_K ;

!! 

LEXICON VERB_K_CASE
 VERB_K ;
+Loc:%>т VERB_K ;
+Lat:%>н VERB_K ;

LEXICON VERB_K
@U.VPref.ZERO@@D.ERRORTH.nohyphen@:@U.VPref.ZERO@@D.ERRORTH.nohyphen@ K ;
@R.ERRORTH.nohyphen@+Err/Orth-no-hyphen:@R.ERRORTH.nohyphen@	VERBS_WITH_PREFIX ;
@D.ERRORTH.nohyphen@:@D.ERRORTH.nohyphen@	VERBS_WITH_PREFIX ;
	
LEXICON VERBS_WITH_PREFIX
@R.VPref.akwan@+Pref/akwan:@R.VPref.akwan@ VPrefixes_END ;
@R.VPref.jol@+Pref/jol:@R.VPref.jol@ VPrefixes_END ;
@R.VPref.jot@+Pref/jot:@R.VPref.jot@ VPrefixes_END ;
@R.VPref.kon@+Pref/kon:@R.VPref.kon@ VPrefixes_END ;
@R.VPref.konalj@+Pref/konalj:@R.VPref.konalj@ VPrefixes_END ;
@R.VPref.laakkwa@+Pref/laakkwa:@R.VPref.laakkwa@ VPrefixes_END ;
@R.VPref.lap@+Pref/lap:@R.VPref.lap@ VPrefixes_END ;
@R.VPref.ljaaljt@+Pref/ljaaljt:@R.VPref.ljaaljt@ VPrefixes_END ;
@R.VPref.naaluw@+Pref/naaluw:@R.VPref.naaluw@ VPrefixes_END ;
@R.VPref.noox@+Pref/noox:@R.VPref.noox@ VPrefixes_END ;
@R.VPref.paag@+Pref/paag:@R.VPref.paag@ VPrefixes_END ;
@R.VPref.paalyg@+Pref/paalyg:@R.VPref.paalyg@ VPrefixes_END ;
@R.VPref.pooxan@+Pref/pooxan:@R.VPref.pooxan@ VPrefixes_END ;
@R.VPref.pulig@+Pref/pulig:@R.VPref.pulig@ VPrefixes_END ;
@R.VPref.raawyg@+Pref/raawyg:@R.VPref.raawyg@ VPrefixes_END ;
@R.VPref.supyg@+Pref/supyg:@R.VPref.supyg@ VPrefixes_END ;
@R.VPref.taara@+Pref/taara:@R.VPref.taara@ VPrefixes_END ;
@R.VPref.tuw@+Pref/tuw:@R.VPref.tuw@ VPrefixes_END ;
@R.VPref.xomi@+Pref/xomi:@R.VPref.xomi@ VPrefixes_END ;
@R.VPref.xot@+Pref/xot:@R.VPref.xot@ VPrefixes_END ;
@R.VPref.xoot@+Pref/xoot:@R.VPref.xoot@ VPrefixes_END ;
@R.VPref.eel@+Pref/eel:@R.VPref.eel@ VPrefixes_END ;
@R.VPref.eelalj@+Pref/eelalj:@R.VPref.eelalj@ VPrefixes_END ;
@R.VPref.juw@+Pref/juw:@R.VPref.juw@ VPrefixes_END ;
@R.VPref.juwle@+Pref/juwle:@R.VPref.juwle@ VPrefixes_END ;
@R.VPref.juil@+Pref/juil:@R.VPref.juil@ VPrefixes_END ;

LEXICON VPrefixes_END
@R.VPref1.at@@U.VPref2.ZERO@+Neg:@R.VPref1.at@@U.VPref2.ZERO@ K ;
@R.VPref1.ul@@U.VPref2.ZERO@+Neg:@R.VPref1.ul@@U.VPref2.ZERO@ K ;
@U.VPref1.ZERO@@U.VPref2.ZERO@:@U.VPref1.ZERO@@U.VPref2.ZERO@ K ;
!@U.VPref1.ZERO@ K ;

@R.VPref1.at@+Neg:@R.VPref1.at@ VPrefixes_END_1 ;
@R.VPref1.ul@+Neg:@R.VPref1.ul@ VPrefixes_END_1 ;
@U.VPref1.ZERO@ VPrefixes_END_1 ;!CHECKME 2023-11-19

@U.VPref1.ZERO@@R.VPref2.vos@+Aux/vos:@U.VPref1.ZERO@@R.VPref2.vos@ K ;
@U.VPref1.ZERO@@R.VPref2.ke@+Disc/ke:@U.VPref1.ZERO@@R.VPref2.ke@ K ;
@U.VPref1.ZERO@@R.VPref2.te@+Disc/te:@U.VPref1.ZERO@@R.VPref2.te@ K ;
@U.VPref1.ZERO@@R.VPref2.ta@+Disc/ta:@U.VPref1.ZERO@@R.VPref2.ta@ K ;
@U.VPref1.ZERO@@R.VPref2.ty@+Disc/ty:@U.VPref1.ZERO@@R.VPref2.ty@ K ;
@U.VPref1.ZERO@@R.VPref2.ZERO@:@U.VPref1.ZERO@@R.VPref2.ZERO@ K ;

LEXICON VPrefixes_END_1
@R.VPref2.te@+Disc/te:@R.VPref2.te@ K ;
@R.VPref2.ke@+Disc/ke:@R.VPref2.ke@ K ;
@R.VPref2.ta@+Disc/ta:@R.VPref2.ta@ K ;
@R.VPref2.ty@+Disc/ty:@R.VPref2.ty@ K ;
@R.VPref2.vos@+Aux/vos:@R.VPref2.vos@ K ;

